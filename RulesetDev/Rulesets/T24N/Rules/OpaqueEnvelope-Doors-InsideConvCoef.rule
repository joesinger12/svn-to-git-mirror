// Fenestration - Interior Air Film Convection Coefficient Adjustments
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN if
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// ----------------------------------------------------------------------------
//  

// The main objects in the SDD are:
//    DoorConstruction:UFactorSim
//    Door:InsideConvCoef

// --------------  --------------------
// Input restriction summary: 
//  None - All properties in this ruleset are considered 'NotInput' and
//  are set by the rulset
//
//      
//  In cases where the input U-Factor exceeds the E+ upper limit of 1.16 Btu/hr-ft2-F
//  the 'Modified Interior Film Coefficient' method is triggered, for both the Porposed
//  and Standard Design u-factor simulation inputs.
//  This method limits the simulation input U-factor to 1.16 Btu/hr-ft2-F (6.53 W/m2-K), 
//  and also provides a fixed interior convection coefficient calculated based on the 
//  input u-factor:  
//    For input U-factor <= 1.16, InsideConvCoeff =  ( 1.447376 * ( DrCons:UFactor ) * ( DrCons:UFactor ) + (-1.520780 * ( DrCons:UFactor ) ) + 3.626257 )
//    For input U-factor > 1.16,  InsideConvCoeff =  ( 4.002783 * ( DrCons:UFactor ) * ( DrCons:UFactor ) + (-0.442014 * ( DrCons:UFactor ) ) - 0.928115 )


// ---------- Flag to trigger use InsideConvCoeff method regardless of U-factor

RULE Dr:DebugInsideConvCoefFlag
  DESCRIPTION
    "Set this flag to value of 1 if the debug mode is checked"
  HELP
  	"Set this flag to value 1 in order to force the door to use fixed inside
  	 convection coefficient method for simulating door U-factor."
  INPUTCLASS
    Prescribed
  DEFAULT
  	0
;  comment out the sections below to enable use of this flag.
;  property to be specified in CBECC input file
  SIZING
    0
  ANNUAL
    z:DebugInsideConvCoefFlag
ENDRULE

// ---------- Identify DrCons for which InsideConvCoeff method is used -------
RULE NEW Dr:UFactorLimitFlag
  DATATYPE
    Integer
  LONGFORM
    UFactorLimitFlag
  DESCRIPTION
    "A Flag to indicate that the user specified or default U-Factor 
     exceeds the Energy-Plus simulation effective limit of 6.53 W/m2-K
     or 1.16 Btu/h-ft2-F"  
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( IfValidAnd( UFactor > 1.16 ) .OR.
        IfValidAnd( BaseUfactor > 1.16) .OR. 
        DebugInsideConvCoefFlag = 1 )
    then 1
    else 0
    endif 
  SIZING_BASELINE
    zp:UFactorLimitFlag
  ANNUAL
    z:UFactorLimitFlag
ENDRULE

// ---------- Set UFactorSim - the value that is sent to simulation -----------
RULE DrCons:UFactorSim
;  DATATYPE
;    Float
;  LONGFORM
;    UFactorSimulated
  DESCRIPTION
    "Intermediate calculation to limit the simulated U-factor to the EnergyPluse 
     Effective Maximum U-Factor = 1.16 Btu/hr-ft2-F or 6.53 W/m2-K. This is the
     value that will be passed to OpenStudio for simulation."  
  INPUTCLASS
    NotInput
  SIZING
    if( UFactor > 1.16 )
    then // Set an upper limit on u-factor (1.16 Btu/hr-ft2-F).
      1.16
    else // FDrCons:or input u-foctor below 1.16 Btu/hr-ft2-F, maintain input value
      UFactor
    endif  
  ANNUAL
    z:UFactorSim
ENDRULE

// ********** Door Inside Convection Coefficient *******************
RULE Dr:InsideConvCoef
  DESCRIPTION
  	"For doors using the modified inside convection coefficient u-factor input method, 
     the calculated value of inside convection coefficient provided to the  E+ simulation
     engine."
  HELP
    "Calculation to set the Interior AirFilm Coefficient value, in cases where the
     user input U-value is greater than the E+ simulation limitations:  
     Effective Maximum of 1.16 Btu/hr-ft2-F"  
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-ft2-F
  SIZING
    if( UFactorLimitFlag = 1 )
    then if( UFactor > 1.16 )
      then ; Calculated Inside Convection Coefficient for u-factor > 1.16 Btu/hr-ft2-F
           ( ( 4.002783 * ( UFactor ) * ( UFactor ) +
           (-0.442014 * ( UFactor ) ) - 0.928115 ) )
           / 5.6783
      else ; Calculated Inside Convection Coefficient for u-factor < 1.16 Btu/hr-ft2-F
           ( ( 1.447376 * ( UFactor ) * ( UFactor ) +
           (-1.520780 * ( UFactor ) ) + 3.626257 ) )
           / 5.6783
      endif 
    else UNCHANGED
    endif
  ANNUAL
    z:InsideConvCoef
ENDRULE

// ---------- Project level flags identifying use of InsideConvCoeff method ---

// LH - removed from CBECC-Com for 2013-v3e and 2016.02
;RULE Proj:ExcptCondUFactorLimit
;  DESCRIPTION
;    "A Flag to indicate whether any user specified U-Factor exceeds the Energy-Plus 
;     simulation effective upper limit of 1.16 Btu/hr-ft2-F or 6.53 W/m2-K"  
;  HELP
;    ""  
;  REFERENCE 
;    
;  INPUTCLASS
;    NotInput
;  SIZING
;    if( SumChildren( DrCons:UFactorLimitFlag ) > 0 )
;    then "Yes"
;    else "No"
;    endif            
;  ANNUAL
;    z:ExcptCondUFactorLimit
;ENDRULE

RULE NEW Dr:DebugInsideConvCoefUsed
  DATATYPE
    Integer
  LONGFORM
    DebugInsideConvCoefUsed
  DESCRIPTION
    "A flag to indicate that 'DebugInsideConvCoeffFlag' is set to
     1 (TRUE) for one or more REFERENCED door constructions which would 
     otherwise be simulated using the E+ Native 'Standard Door Model'."  
  INPUTCLASS
    NotInput
  DEFAULT
    if( DebugInsideConvCoefFlag = 1 .AND.
        IfValidAnd( UFactor <= 1.16 ) .AND.
        IfValidAnd( Area > 0 ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW Proj:DebugInsideConvCoefUsed
  DATATYPE
    Integer
  LONGFORM
    DebugInsideConvCoefUsed
  DESCRIPTION
    "A project level flag to indicate that 'DebugInsideConvCoeffFlag' is set to
     1 (TRUE) for one or more REFERENCED door constructions which would 
     otherwise be simulated using the E+ Native 'Standard Door Model'."  
  INPUTCLASS
    NotInput
  DEFAULT
    if( SumAll( Dr:DebugInsideConvCoefUsed ) > 0 )
    then 1
    else 0
    endif
ENDRULE
