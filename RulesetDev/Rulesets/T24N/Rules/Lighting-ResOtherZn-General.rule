// Interior Lighting - General Information
//
// -------------------------------------------------------------------------
//  Copyright (c ) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION ) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE ) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN if
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------

// ---------- Lighting Classification Method ----------------------------------
RULE ResOtherZn:IntLtgSpecMthd
  DESCRIPTION
    "The method for selecting baseline lighting and other loads for a zone"  
  INPUTCLASS
    Default
  DEFAULT : T24N
    "AreaCategoryMethod"
ENDRULE

// ---------- Whether the lighting is specified via Interior Lighting System --
RULE NEW ResOtherZn:IntLtgSysChildren
  DESCRIPTION
    "The number of interior lighting system children."  
  DATATYPE 
    Integer
  INPUTCLASS
    NotInput
  DEFAULT 
    ChildCount( IntLtgSys )
  ANNUAL
    UNDEFINED
ENDRULE

// ********** Lighiting Status ************************************************
// Flags to identify whether 'user' defined or standard design lighting power 
// is to be utilized

// ---------- Build Up Zone Status parameters based on IntLtgSys inputs ------
RULE NEW ResOtherZn:NewLumCnt
  DATATYPE
    Integer
  LONGFORM
    NewLuminaireCount
  DESCRIPTION
    "The number of Luminaires with Status = 'New' assigned
     to the zone."  
  INPUTCLASS
    NotInput
  DEFAULT
    SumChildren( IntLtgSys:NewLumCnt )
  SIZING
    UNDEFINED
ENDRULE
RULE NEW ResOtherZn:AltLumCnt
  DATATYPE
    Integer
  LONGFORM
    AlteredLumCount
  DESCRIPTION
    "The number of luminaires with Status = 'Altered' 
     assigned to the zone."  
  INPUTCLASS
    NotInput
  DEFAULT
    SumChildren( IntLtgSys:AltLumCnt )
  SIZING
    UNDEFINED
ENDRULE
RULE NEW ResOtherZn:ExistLumCnt
  DATATYPE
    Integer
  LONGFORM
    ExistingLuminaireCount
  DESCRIPTION
    "The number of luminaires with Status = 'Existing' 
     assigned to the zone."  
  INPUTCLASS
    NotInput
  DEFAULT
    SumChildren( IntLtgSys:ExistLumCnt )
  SIZING
    UNDEFINED
ENDRULE
RULE NEW ResOtherZn:FutureLumCnt
  DATATYPE
    Integer
  LONGFORM
    FutureLuminaireCount
  DESCRIPTION
    "The number of child luminaires with Status = 'Future' 
     assigned to the zone."  
  INPUTCLASS
    NotInput
  DEFAULT
    SumChildren( IntLtgSys:FutureLumCnt )
  SIZING
    UNDEFINED
ENDRULE
RULE NEW ResOtherZn:TotLumCnt
  DATATYPE
    Integer
  LONGFORM
    TotalLuminaireCount
  DESCRIPTION
    "The number of Luminaires assigned to the zone."  
  INPUTCLASS
    NotInput
  DEFAULT
    NewLumCnt + AltLumCnt + ExistLumCnt + FutureLumCnt
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:AltLumFrac
  DATATYPE
    Float
  LONGFORM
    AlteredLuminaireFraction
  DESCRIPTION
    "The percentage of luminaires in the zone which have been altered.  Determines
     whether standard design will be existing lighting or prescriptive allowance."  
  INPUTCLASS
    NotInput
  DEFAULT
    if( TotLumCnt > 0 )
    then AltLumCnt / TotLumCnt
    else 0
    endif
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:SpcOverallLumStatus
  RULESETS
    T24N
  DATATYPE
    String
  LONGFORM
    SpaceOverallLuminaireStatus
  DESCRIPTION
    "The aggregated status of the child interior lighting systems associated
     assigned to the zone"  
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( IntLtgSysChildren > 0 ) )
    then    // Get Status from IntLtgSys
      if( NewLumCnt >= 1 ) 
      then "New"
      else if( AltLumCnt >= 1 ) 
      then if (AltLumFrac < .1 )
        then "Existing"
        else "Altered"
        endif
      else if( ExistLumCnt >= 1 ) 
      then "Existing" 
      else if( FutureLumCnt >= 1 ) 
      then "Future" 
      else "New"
      endif endif endif endif
    else UNCHANGED
    endif
// TO DO  CHECKCODE
;     if( IsAlt )
;     then if( AltLumCnt > 0 .AND. AltLumFrac < 0.10 )
;       then 
;         PostWarning( "In Space: '%s', The fraction of 'Altered' luminaires is
;                      less than 10%.  Lighting will be treated as existing.",
;                      Name)
;       else if( AltLumFrac >= 0.10 )
;       then
;         PostWarning ( "In Space: '%s', The fraction of 'Altered' luminaires is
;                        greater than or equal to 10%.  The standard design lighting
;                        power density will be set to the prescriptive maximum
;                        for the selected space function.",
;                        Name)
;       else UNCHANGED
;       endif endif
;     else UNCHANGED
;     endif    
  SIZING
    UNDEFINED
ENDRULE

// ********** ResOtherZn - Lighting Status *****************************************
// Rules to identify whether 'user' defined lighting power is to be utilized
// for propsed and/or standard design.

RULE NEW ResOtherZn:DefaultLtgStatus
  DATATYPE 
    Enumeration
  LONGFORM
    DefaultLightingStatus
  DESCRIPTION
    "This property holds the default Lighting Status based on project compliance
     and envelope status."  
  INPUTCLASS
    NotInput
  OPTION
    New
    Altered
    Existing
    Future
  DEFAULT 
    if( IntLtgSysChildren > 0 )
    then "New" ; SpcOverallLumStatus
    else if( Proj:IsNewLtg )
    then "New"
    else if( Proj:IsExistingLtg )
    then "Existing"
    else if( Proj:IsFutureLtg )
    then "Future"           
    else if( Proj:CompType = "ExistingAlteration" .OR.
             Proj:CompType = "ExistingAdditionAndAlteration" .OR.
             Proj:CompType = "AdditionComplete" .OR.
             Proj:CompType = "AdditionEnvelopeAndPartialLighting" .OR.
             Proj:CompType = "AdditionEnvelopeAndLighting" )
    then "New"
    else if( Proj:CompType = "NewEnvelopeAndPartialLightingAndPartialMechanical" .OR.
             Proj:CompType = "AdditionEnvelopeAndPartialLightingAndPartialMechanical" )
    then "Existing"
    else if( Proj:CompType = "AdditionEnvelope" .OR.
             Proj:CompType = "AdditionEnvelopeAndMechanical" )
    then "Future"
    else "New"
    endif endif endif endif endif endif endif
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:LtgStatus
// Proj:CompType options:
//    ComplianceType                         Valid Options                Default (* indicates the only option)
//    --------------                         -------------                -------
//    NewComplete                            New                          *New
//    NewEnvelope                            Future                       *Future
//    NewEnvelopeAndLighting                 New                          *New     
//    NewEnvelopeAndPartialLighting          New, Future                  New    
//    NewEnvelopeAndMechanical               Future                       *Future
//    NewMechanical                          Existing                     *Existing     
//    NewMechanicalAndLighting               New                          *New     
//    NewMechanicalAndPartialLighting        New, Existing                New
//    ExistingAlteration                     New, Altered, Existing       REF:EnvStatus
//    ExistingAdditionAndAlteration          New, Altered, Existing       REF:EnvStatus
//    AdditionComplete                       New, Existing                REF:EnvStatus
//    AdditionEnvelope                       Future, Exist                If(EnvStatus = New) then Future, else Existing
//    AdditionEnvelopeAndLighting            New, Existing                REF:EnvStatus
//    AdditionEnvelopeAndPartialLighting     New, Existing, Future,       REF:EnvStatus
//    AdditionEnvelopeAndMechanical          Future, Exist                If(EnvStatus = New) then Future, else Existing
//    AdditionMechanical                     Existing                     *Existing
//    AdditionMechanicalAndLighting          New, Existing                New
//    AdditionMechanicalAndPartialLighting   New, Future,                 New
  DESCRIPTION
    "Specifies the Status of the lighting in a space for Additions, Alterations
     and Partial Compliance."
  HELP
    "Used to determine whether user specified or standard design values for 
     lighting power are installed in the proposed and baseline models.

      New:       Applies to lighting in 'New' Spaces or lighting in 'Altered'
                 spaces wheere th following conditions are met: 
                    a) Alteration increases space area.
                    b) Alteration increases total lighting power in a space.

      Altered:   Valid only for ExistingAlteration and 
                 ExistingAdditionAndAlteration compliance types. Applies to 
                 luminaire modifications in existing spaces, which do not 
                 increase the total space lighting power.
                  
      Existing:  Applies to NewMechanical or NewMechanicalAndPartialLighting
                 Compliance, in which Lighting systems have been evaluated in 
                 in a previous compliance evaluation.  Also applies to 
                 Additions/Alterations Compliance where lighting systems are
                 not being modified as part the addition/alteration.

      Future:    In NewEnvelope, NewEnvelopeAndPartialLighting, or
                 NewEnvelopeAndMechanical compliance, where Lighting
                 is not part of the current compliance evaluation, Lighting
                 Power Density inputs with Status = Future will be
                 ignored."

  INPUTCLASS
    Default
  OPTION 
    New
    Altered
    Existing
    Future
  DEFAULT
    DefaultLtgStatus
  SIZING : T24N
    if( IsNewLtg = 1 ) then "New"  else 
    if( IsAlteredLtg = 1 ) then "Altered" else
    if( IsExistingLtg = 1 ) then "Existing" else
    if( IsFutureLtg = 1 ) then "Future" else "New"
    endif endif endif endif
  ENDRULE

RULE NEW ResOtherZn:IsValidLtgStatus
  DATATYPE 
    Integer
  LONGFORM
    IsValidLightingStatus
  DESCRIPTION
    "This flag identifies whether the user selected luminaire status is not valid."  
  INPUTCLASS
    NotInput
  DEFAULT 
    if(IfValidAnd( LtgStatus = "New" ) )
    then ValidCompStatus:Ltg_New( "ComplianceType", Proj:CompType )
    else if( IfValidAnd( LtgStatus = "Altered" ) )
    then ValidCompStatus:Ltg_Altered( "ComplianceType", Proj:CompType )
    else if( IfValidAnd( LtgStatus = "Future" ) )
    then ValidCompStatus:Ltg_Future( "ComplianceType", Proj:CompType )
    else if( IfValidAnd( LtgStatus = "Existing" ) )
    then if( Proj:CompType = "AdditionEnvelopeAndPartialLightingAndPartialMechanical" )
         then 1
         else ValidCompStatus:Ltg_Existing( "ComplianceType", Proj:CompType )
         endif
    else 0
    endif endif endif endif
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:LtgStatusForComp
  RULESETS
    T24N
  DATATYPE
    String
  LONGFORM
    StatusForCompliance
  DESCRIPTION
    "A string to hold the true lighting status for the default/checkcode transform"  
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNewLtg ) then "New" else 
    if( Proj:IsExistingLtg ) then "Existing" else
    if( Proj:IsFutureLtg ) then "Future" else
    if( IsValidLtgStatus ) then LtgStatus else
    DefaultLtgStatus
    endif endif endif endif 
  CHECKCODE
	  if( IsValidLtgStatus = 0 )
    then
      PostWarning( "For ResOtherZn '%s': Lighting status '%s'is not valid for the 
                    selected compliance type: '%s'.  Lighting Status has been 
                    reset to '%s'.",
                    Name, LtgStatus, Proj:CompType, LtgStatusForComp )
     else UNCHANGED
     endif
ENDRULE

RULE NEW ResOtherZn:IsNewLtg
  DATATYPE
    Integer
  LONGFORM
    IsNewLighting
  DESCRIPTION
    "A flag to indicate whether the lighting in a zone has 'New' status."  
  INPUTCLASS
    NotInput
  DEFAULT
	  if ( IfValidAnd( LtgStatusForComp = "New" ) )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:IsAlteredLtg
  DATATYPE
    Integer
  LONGFORM
    IsAlteredLighting
  DESCRIPTION
    "A flag to indicate whether the lighting in a zone has 'Altered' status."  
  INPUTCLASS
    NotInput
  DEFAULT
	  if ( IfValidAnd( LtgStatusForComp = "Altered" ) )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:IsExistingLtg
  DATATYPE
    Integer
  LONGFORM
    IsExistingLighting
  DESCRIPTION
    "A flag to indicate whether the lighting in a zone has 'Existing' status."  
  INPUTCLASS
    NotInput
  DEFAULT
	  if ( IfValidAnd( LtgStatusForComp = "Existing" ) )
    then 1
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:IsFutureLtg
  DATATYPE
    Integer
  LONGFORM
    IsExistingLighting
  DESCRIPTION
    "A flag to indicate whether the lighting in a zone has 'Existing' status."  
  INPUTCLASS
    NotInput
  DEFAULT 
	  if ( IfValidAnd( LtgStatusForComp = "Future" ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ResOtherZn:ApplyStdLtg
  DATATYPE 
    Integer
  LONGFORM
    ApplyStandardLighting
  DESCRIPTION
    "Flag to identify whether standard LPD is applied."  
  INPUTCLASS
    NotInput
  DEFAULT : T24N
    if( IsFutureLtg ) 
    then  // envelope only compliance, proposed lighting subject to standard design rules
      1
    else
      0  // all other lighting statuses subject to proposed rules
    endif
  SIZING_PROPOSED : T24N
    if( IsFutureLtg ) 
    then  // proposed lighting subject to standard design rules, such as in 'NewEnvelope' compliance
      1
    else
      0  // all other lighting statuses subject to proposed rules
    endif
  SIZING_BASELINE : T24N
    if( IsExistingLtg )
    then // for existing lighting, standard design is set equal to proposed
      0 
    else 
      1
    endif
ENDRULE

// ********** Baseline Lighting Powers - ResOtherZn ********************************
// calculate the baseline lighting power allowance for a zone, 
// stored at proposed transform for reporting on PERF-1 Reports

// ---------- Area Category Method Baseline - T24N ----------------------------

RULE NEW ResOtherZn:ACMBaseGenLPD
  DATATYPE
    Float
  LONGFORM
    AreaCategoryMethodGeneralLPD
  DESCRIPTION
    "A container for the Baseline General Area Category Method LPD
     at the ResOtherZn level."
  HELP
    "Primarily for reporting purposes, this value represents the baseline LPD
     when using the Area Category Method"
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( SpcFunc = "- specify -" ) = 0 )
    then SpaceFunctionData:IntLPDReg( "FuncType", SpcFunc )
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:ACMBaseGenPwr
  DATATYPE
    Float
  LONGFORM
    AreaCategoryMethodGeneralPower
  DESCRIPTION
    "A container at the ResOtherZn level for the Regulated Lighting Power 
     Calculated via the Area Category Method"
  INPUTCLASS
    NotInput
  DEFAULT
    ACMBaseGenLPD * Area
ENDRULE

// ---------- Tailored Method Baseline - T24N ---------------------------------

RULE NEW ResOtherZn:TMAllowed
  RULESETS
    T24N
  DATATYPE
    Integer
  LONGFORM
     TailoredMethodAllowed
  DESCRIPTION
    "A flag that indicates whether the use of tailored method is allowed for the 
     selected space type."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( SpcFunc != "- specify -" ) )
    then TailoredMethodLPA:TMAllowed( "SpaceFunction", SpcFunc )
    else 0
    endif
  CHECKCODE
    if( LocalStatus( SpcFunc ) > 0 .AND.
        IfValidAnd( IntLtgSpecMthd = "TailoredMethod" ) .AND.
        IfValidAnd( TMAllowed = 0 ) )
    then // Tailored Method only allowed for 
      PostError( "In ResOtherZn '%s', TailoredMethod is not a valid interior lighting
                  specification method for Space Function: '%s'.",
                  Name, SpcFunc )
    else if( IfValidAnd( IntLtgSpecMthd = "TailoredMethod" ) .AND.
             LocalStatus( Perim ) <= 0  )
    then 
      PostError( "In ResOtherZn '%s', zone Perimeter must be specified for Tailored
                  Method interior lighting calculations.",
                  Name )    
    else 
      UNCHANGED
    endif endif
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:TMFlag
  RULESETS
    T24N
  DATATYPE
    Integer
  LONGFORM
    TailoredMethodFlag
  DESCRIPTION
    "A Flag for zones using the Tailored Lighting Method"
  HELP
    "Primarily for QA purposes, this flag will be used to identify whether
     any zones in the project are using the Tailored Lighting Method"
  INPUTCLASS
    NotInput
  DEFAULT
    if ( IntLtgSpecMthd = "TailoredMethod") 
    then 1
    else 0
    endif
ENDRULE
// evaluated in Lighting-Space-General.rule
; RULE NEW Proj:TMFlag 
;   RULESETS
;     T24N
;   DATATYPE
;     Integer
;   LONGFORM
;     TailoredMethodFlag
;   DESCRIPTION
;     "A Flag to identify whether whether any Spaces in the project are using 
;      the Tailored Lighting Method"
;   INPUTCLASS
;     NotInput
;   DEFAULT
;     if( ( SumChildren( Spc:TMFlag ) + SumChildren( ResOtherZn:TMFlag ) ) > 0 )
;     then 1
;     else 0
;     endif
; ENDRULE

RULE NEW ResOtherZn:TMGenAveLumHgt
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
     TailoredMethodGeneralLightingAverageLuminaireHeight
  DESCRIPTION
    "The Average Luminaire Height of all IntLgtSys( s ) for which Tailored Method
     General LPD (Not Additional Allowance) calcs are being performed."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IntLtgSpecMthd = "TailoredMethod" )
    then if( IntLtgSysChildren > 0 .AND.
             SumChildrenIf( IntLtgSys:Pwr,
                            IntLtgSys:AllowType = "GeneralAllowance") > 0 )
      then SumChildrenIf( IntLtgSys:LumMntgHgtXPwr,
                         IntLtgSys:AllowType = "GeneralAllowance")/
           SumChildrenIf( IntLtgSys:Pwr,
                         IntLtgSys:AllowType = "GeneralAllowance")
      else min( 10, CeilingHeight )
      endif
    else UNDEFINED
    endif
ENDRULE
RULE NEW ResOtherZn:TMGenAveWorkPlaneHgt
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
    TailoredMethodAverageWorkplaneHeight 
  DESCRIPTION
    "The Average Work Plane Height of all IntLgtSys( s ) for which Tailored Method
     General LPD (Not Additional Allowance) calcs are being performed."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IntLtgSpecMthd = "TailoredMethod")
    then if( IntLtgSysChildren > 0  .AND.
             SumChildrenIf( IntLtgSys:Pwr,
                            IntLtgSys:AllowType = "GeneralAllowance") > 0)
      then SumChildrenIf( IntLtgSys:WorkPlaneHgtXPwr,
                         IntLtgSys:AllowType = "GeneralAllowance")/
           SumChildrenIf( IntLtgSys:Pwr,
                         IntLtgSys:AllowType = "GeneralAllowance")
      else 3
      endif
    else UNDEFINED
    endif
ENDRULE
RULE NEW ResOtherZn:TMGenTaskHgt
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
      TailoredMethodGeneralTaskHeight
  DESCRIPTION
    "The Task Height all IntLgtSys( s ) for which Tailored Method
     General LPD (Not Additional Allowance) calcs are being performed.
     Task Height is defined as the distance from the work plane to center 
     of the lighting fixture"
  INPUTCLASS
    NotInput
  DEFAULT   
    if( LocalStatus( TMGenAveLumHgt ) = 0 .OR.
       LocalStatus( TMGenAveWorkPlaneHgt ) = 0 )
    then UNDEFINED
    else TMGenAveLumHgt - TMGenAveWorkPlaneHgt
    endif
ENDRULE
RULE NEW ResOtherZn:RCR
  DATATYPE
    Float
  DESCRIPTION
    "The Room Cavity Ratio of a zone calculated as:
     2.5*Perimeter*TaskHeight/Area"
  REPORTPRECISION
    1
  DEFAULT
    if( Area > 0 .AND.
        LocalStatus( Perim ) > 0 .AND.
        LocalStatus( TMGenTaskHgt ) > 0 )
    then 2.5*( Perim )*TMGenTaskHgt / Area
    else UNDEFINED
    endif
ENDRULE
RULE NEW ResOtherZn:TMRCRCat
  RULESETS
    T24N
  DATATYPE 
    String                 
  LONGFORM
     TailoredMethodRoomCavityRatioCategory
  DESCRIPTION
    "For table lookups in Table 140.6-G"
  INPUTCLASS
    NotInput
  DEFAULT
    if ( IntLtgSpecMthd = "TailoredMethod") 
    then if( LocalStatus( RCR ) = 0 )
      then UNDEFINED
      else if ( RCR <= 2.0 ) then "RCRCat1"
      else if ( RCR <= 3.5 ) then "RCRCat2"
      else if ( RCR <= 7.0 ) then "RCRCat3"
      else "RCRCat4"
      endif endif endif endif
    else UNDEFINED
    endif
ENDRULE

RULE NEW ResOtherZn:TMGenIlum
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
    TailoredMethodGeneralIllumination
  DESCRIPTION
    "A container at the ResOtherZn level for the Specified Illumination Level 
     for Tailored Method baseline lighting power calcs"
  INPUTCLASS
    NotInput
  DEFAULT
    if ( IntLtgSpecMthd = "TailoredMethod" .AND.
         TMAllowed = 1 ) 
    then TailoredMethodLPA:GenIllum("SpaceFunction", SpcFunc )
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:TMGenlIlumRnd
  RULESETS
    T24N
  DATATYPE
    Float
  DESCRIPTION
    "Rounded Tailored Method Illumination for use in lookups for Table 146-B"
  INPUTCLASS
    NotInput
  DEFAULT
    if ( TMGenIlum > 0 )
    then MAX( TMGenIlum-mod( TMGenIlum,100 ),150 )
    else 0
    endif 
ENDRULE

RULE NEW ResOtherZn:TMBaseGenlLPD
  RULESETS
    T24N
  DATATYPE
    Float
  DESCRIPTION
    "A container for the General Tailored Method LPD
     at the ResOtherZn level."
  INPUTCLASS
    NotInput 
  REPORTPRECISION
    1
  DEFAULT
    if( IntLtgSpecMthd = "TailoredMethod" .AND.
        TMAllowed = 1 .AND. 
        LocalStatus( TMRCRCat ) > 0 .AND.
        LocalStatus( TMGenlIlumRnd ) > 0 )
    then IlluminanceLevelLPD:LPD("RCRCat",TMRCRCat,
                                   "IllumLevel",TMGenlIlumRnd )
    else 0
    endif
ENDRULE
RULE NEW ResOtherZn:TMBaseGenlPwr 
  RULESETS
    T24N
  DATATYPE
    Float
  DESCRIPTION
    "A container at the ResOtherZn level for the Regulated Lighting Power 
     Calculated via the Tailored Method"
  INPUTCLASS
    NotInput
  REPORTPRECISION
    0
  DEFAULT
    TMBaseGenlLPD * Area
ENDRULE
// evaluated in Lighting-Space-General.rule
; RULE Bldg:TMBaseGenlPwr 
;   RULESETS
;     T24N
;   DESCRIPTION
;     "A container at the building level for the Regulated Lighting Power 
;      Calculated via the Tailored Method"
;   INPUTCLASS
;     NotInput
;   REPORTPRECISION
;     0
;   DEFAULT
;     SumChildren( Spc:TMBaseGenlPwr ) + SumChildren( ResOtherZn:TMBaseGenlPwr )
; ENDRULE
; RULE Bldg:CondTMBaseGenlPwr 
;   RULESETS
;     T24N
;   DESCRIPTION
;     "A container at the building level for the Regulated Baseline Lighting Power 
;      Allowance Calculated via the Tailored Method, for conditioned spaces"
;   INPUTCLASS
;     NotInput
;   REPORTPRECISION
;     0
;   DEFAULT
;     SumChildrenIf( Spc:TMBaseGenlPwr, Spc:IsCond ) + SumChildrenIf( ResOtherZn:TMBaseGenlPwr, ResOtherZn:Type = "Conditioned" )
; ENDRULE
; RULE Bldg:UncondTMBaseGenlPwr 
;   RULESETS
;     T24N
;   DESCRIPTION
;     "A container at the building level for the Regulated Baseline Lighting Power 
;      Allowance Calculated via the Tailored Method, for unconditioned spaces"
;   INPUTCLASS
;     NotInput
;   REPORTPRECISION
;     0
;   DEFAULT
;     SumChildrenIf( Spc:TMBaseGenlPwr, Spc:IsCond = 0 ) + SumChildrenIf( ResOtherZn:TMBaseGenlPwr, ResOtherZn:Type = "Unconditioned" )
; ENDRULE

// ********** General Lighting Power - ResOtherZn **********************************

RULE NEW ResOtherZn:RegBaseGenLPD
  DATATYPE
    Float
  LONGFORM
    RegulatedBaselineGeneralLightingPowerDensity
  DESCRIPTION
    "A container for the Regulated Baseline Regulated Baseline General Lighting Power Density"
  INPUTCLASS
    NotInput
  DEFAULT : T24N
    if ( IntLtgSpecMthd = "AreaCategoryMethod") 
    then ACMBaseGenLPD
    else if ( IntLtgSpecMthd = "TailoredMethod") 
    then TMBaseGenlLPD
    else 0
    endif endif 
ENDRULE
RULE NEW ResOtherZn:RegBaseGenPwr
  DATATYPE
    Float
  LONGFORM
    RegulatedBaselineGeneralPower
  DESCRIPTION
    "A container for the Regulated Baseline RegBaseGenPwrGeneral Lighting Power"
  INPUTCLASS
    NotInput
  DEFAULT
    RegBaseGenLPD * Area
ENDRULE

// ********** Regulated Interior Lighting Power - ResOtherZn ***********************
RULE NEW ResWin:MinPresVT
  DATATYPE
    Float
  LONGFORM
    MinimumPrescriptiveVisibleTransmittance
  DESCRIPTION
    "The minimum prescriptive visible transmittance of a Fenestration 
     Construction, if applicable"  
  INPUTCLASS
    NotInput 
  DEFAULT
    switch( WinProdType )
		case "Curtain Wall / Storefront"       : T24RClimateZoneCodeBase:CurtainWallStorefrontVT( "ClimateZone", Proj:CliZnNum, "CodeBase", EnumValue(Proj:StdDesignBase), "BldgType", Proj:BldgTypeTblStr )
		case "NAFS 2017 Performance Class AW"  : T24RClimateZoneCodeBase:NAFS2017ClassAWVT( "ClimateZone", Proj:CliZnNum, "CodeBase", EnumValue(Proj:StdDesignBase), "BldgType", Proj:BldgTypeTblStr )
		default                                : UNDEFINED
		endswitch
ENDRULE
RULE NEW ResWin:LowVTLPDAdjFlag
  DATATYPE
    Integer
  LONGFORM
    LowVisibleTransmittanceLPDAdjustmentFlag
  DESCRIPTION
    "A flag to indicate that the proposed design VT does not meet the minimum 
     prescriptive requirements.  This condition triggers a a baseline LPD
     adjustment in simplified geometry models intended to account for reductions
     to daylighting potential associated with low VT values."  
  DEFAULT
    if( ParentComponentType() = "ResExtWall" .AND. 
      ( Status = "New" .OR. Status = "Altered" ) .AND.
        IfValidAnd( MinPresVT > NFRCVT ) )
    then 1
    else 0
    endif
  CHECKCODE
    if( LowVTLPDAdjFlag > 0.5 )
    then PostWarning( "Residential window '%s' in Space '%s' has visible transmittance
                       less than the prescriptive minimum (VT = %.3f, MinVT = %.3f).
                       This condition triggers a reduction to the baseline LPD
                       by a factor of 0.833 
                       intended to account for reductions to daylight harvesting
                       potential associated with low VT values.
                       ", Name, Parent2( Name ), NFRCVT, MinPresVT ) 
    else UNCHANGED
    endif
ENDRULE
RULE NEW ResOtherZn:LowVTLPDAdj
  DATATYPE
    Float
  LONGFORM
    LowVisibleTransmittanceLPDAdjustment
  DESCRIPTION
    "The value of the baseline LPD adjustment which accounts for reductions
     to daylighting potential associated with low VT values in Simplified 
     geometry models.  A value of 1 indicated no adjustment is made."  
  INPUTCLASS
    NotInput 
  DEFAULT
    if( SumChildren( ResWin:LowVTLPDAdjFlag ) > 0 )
    then 1 / 1.2
    else 1
    endif
  SIZING_PROPOSED
    1  ; proposed design lighting power should not recieve penalty
  SIZING_BASELINE
    u:LowVTLPDAdj
ENDRULE
RULE ResOtherZn:IntLPDReg
  DESCRIPTION
    "User Input - Total regulated connected lighting power density for all
     interior lighting systems in a Space, including the loads for lamps and 
     ballasts."
  HELP
    "This property is utilized for User Input, and may be ignored or overwritten
     under certian circumstances."
  INPUTCLASS 
    Default
  REPORTPRECISION
    1
  DEFAULT
    if( IfValidAnd( SpcFunc = "- specify -" ) = 0 )
    then  ; use ACM default for LPD
      SpaceFunctionData:IntLPDReg("FuncType",SpcFunc)
    else ; No Space Function Assigned
      0
    endif
  SIZING ; will be reset below to maintain current functionality.  Transistioning to Sim properties
    UNDEFINED  
ENDRULE

RULE NEW ResOtherZn:RegLtgPwr
  DESCRIPTION
    "Container for Total Regulated Lighting Power at Space"
  INPUTCLASS       
    NotInput         
  UNITS 
    Watts      
  REPORTPRECISION
    0
  DEFAULT
    if( IntLtgSysChildren > 0 )
    then  ; Power Specified via Interior Lighting System
      SumChildren( IntLtgSys:RegLtgPwr )
    else if( ApplyStdLtg ) 
    then  ; Standard Design Lighting - used in residential and future lighting cases
      ValidOr( RegBaseGenPwr, 0 ) +
      ValidOr( RegAddlAllowPwr, 0 )
    else if(IfValidAnd( Area > 0 ) ) 
    then  ; space level, user input lighting power
       IntLPDReg*Area 
    else
      0 
    endif endif endif
  SIZING
    switch( ApplyStdLtg )
      case 1:    
        if( RegLtgPwr = 0 )     
        then 
          0   ; if User specified lighting is 0, set baseline to 0 as well
        else  ; standard design lighting power, including allowances
          LowVTLPDAdj *
          ( ValidOr( RegBaseGenPwr, 0 ) +
            ValidOr( RegAddlAllowPwr, 0 ) )
        endif
      default: LowVTLPDAdj * RegLtgPwr  ; user specified.
    endswitch      
  ANNUAL
   RegLtgPwr
ENDRULE

RULE ResOtherZn:IntLPDRegSim
  DESCRIPTION
    "Calculated - Total regulated connected lighting power density for all
     interior lighting systems in a Space, including the loads for lamps and 
     ballasts."    
  HELP
    "This property is displayed in the User Interface when lighting power is 
     specified via Interior Lighting System, or when another scenario has
     prevented/overwritten user input."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( Area > 0 ) .AND.
        IfValidAnd(RegLtgPwr >= 0 ) )
    then RegLtgPwr/Area
    else 0
    endif
  SIZING
    if( IfValidAnd( Area > 0 ) .AND.
        IfValidAnd(RegLtgPwr >= 0 ) )
    then RegLtgPwr/Area
    else 0
    endif
  ANNUAL
    IntLPDRegSim
ENDRULE

RULE NEW ResOtherZn:LPDUIDispToggle
  DATATYPE
    Integer
  LONGFORM
    LightingPowerDensityUIDisplayToggle
  DESCRIPTION
    "Toggles which LPD parameter is displayed in the user interface.
       0 -> Display User Input: IntLPDReg
       1 -> Display Calculated: IntLPDRegSim"
  INPUTCLASS       
    NotInput         
  DEFAULT : T24N
    if( IntLtgSysChildren > 0 )            ; overwrite user input with sum of IntLtgSys lighting if IntLtgSys used
    then 1
    else // display user input or default
      0
    endif
  SIZING
    UNDEFINED
ENDRULE

RULE NEW ResOtherZn:IntLtgRegEndUseCat
  DESCRIPTION
    "The end-use sub category into which the regulated lighting energy 
     associated with this space is placed"    
  DATATYPE 
    Enumeration
  INPUTCLASS
    NotInput
  OPTION
    ComplianceLtg
    NonComplianceLtg
  DEFAULT : T24N
    if( Type = "Conditioned" ) 
    then "ComplianceLtg"
    else "NonComplianceLtg"
    endif
ENDRULE

// TO DO ********** Reporting ******************************************************