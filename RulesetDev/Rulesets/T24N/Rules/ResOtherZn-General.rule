// Space - General
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------

// ********** Zone type ********************************************
RULE ResOtherZn:Type
  DESCRIPTION
    "Zone type"
  DEFAULT
    if( SpcFunc = "Parking Garage Area (Parking Zone and Ramps)" .OR. 
        SpcFunc = "Parking Garage Area (Daylight Adaptation Zones)" )
    then "Unconditioned"
    else "Conditioned"
    endif
ENDRULE

RULE ResOtherZn:CondType
  DESCRIPTION
    "Zone condition type"
  DEFAULT
    if( Type = "Conditioned" )
    then "Directly"
    else UNDEFINED
    endif
ENDRULE

// ********** Zone status ********************************************
RULE ResOtherZn:Status
  DESCRIPTION
    "Zone status"
  DEFAULT
    "New"
ENDRULE

; SAC 9/10/19 (MFamProto)
RULE ResOtherZn:HVACSysStatus
  DESCRIPTION
    "Zone HVAC system status"
  DEFAULT
    "New"
ENDRULE

RULE ResOtherZn:DHWSysStatus
  DESCRIPTION
    "Zone DHW system status"
  DEFAULT
    "New"
ENDRULE

// ********** Zone geometry rules ********************************************
RULE ResOtherZn:Mult
  DESCRIPTION
    "Zone multiplier"
  DEFAULT
    if( IfValidAnd( ResZnGrp:Mult > 0 ) )
    then ResZnGrp:Mult
    else 1
    endif
ENDRULE

RULE ResOtherZn:Volume
  DESCRIPTION
    "The volume of the zone."
  DEFAULT
    if ( LocalStatus( CeilingHeight ) < 1 .OR. LocalStatus( Area ) < 1 ) 
    then UNDEFINED
    else CeilingHeight * Area
    endif
ENDRULE

RULE ResOtherZn:Perim
  DESCRIPTION
    "The perimeter of a space."
  DEFAULT
    if( LocalStatus( Area ) < 1 )
    then UNDEFINED
    else sqrt( Area ) * 4
    endif
ENDRULE

RULE ResOtherZn:WinHeadHeight
  DESCRIPTION
    "Average head height of operable windows above the floor"
  DEFAULT
    if( LocalStatus( CeilingHeight ) < 1 )
    then 7.67
    else max( 1, CeilingHeight - 1.33 )
    endif
ENDRULE

RULE ResOtherZn:WinVentHtDiff[1]
  DESCRIPTION
    "Window vent height difference between center of high opening and low opening"
  DEFAULT
    -999.0
ENDRULE

RULE ResOtherZn:WinVentHtDiff[2]
  DESCRIPTION
    "Window vent height difference between center of high opening and low opening"
  DEFAULT
    -999.0
ENDRULE

RULE ResOtherZn:WinVentHtDiff[3]
  DESCRIPTION
    "Window vent height difference between center of high opening and low opening"
  DEFAULT
    -999.0
ENDRULE

RULE ResOtherZn:FloorHeight
  DESCRIPTION
    "Average ceiling height"
  DEFAULT
    max( ValidOr( CeilingHeight, 5 ) + 1, 9 ) ; mod to prevent undefined data error - SAC 05/28/21
ENDRULE

RULE ResOtherZn:Bottom
  DESCRIPTION
    "Height of the top of the lowest floor in this zone / used for Airnet"
  DEFAULT
    if( ChildCount( SlabFloor ) > 0 )
    then 0.67
    else if( ChildCount( FloorOverCrawl ) > 0 )
    then max( 0, ( Global( CrawlSpace:AvgWallHeight ) - 0.67 ) )
    else if( ChildCount( ResExtFlr  ) > 0 )
    then max( 0, MinChild( ResExtFlr:FloorZ ) )
    else if( ChildCount( ResIntFlr  ) > 0 )
    then max( 0, MinChild( ResIntFlr:FloorZ ) )
    else 0
    endif endif endif endif
ENDRULE

RULE ResOtherZn:NumStories
  DESCRIPTION
    "Number of stories in this zone"
  DEFAULT
    1
ENDRULE

RULE ResOtherZn:Top
  DESCRIPTION
    "Z-level of the top of the highest floor in this zone"
  DEFAULT
    Bottom + (FloorHeight * (NumStories - 1)) + ValidOr( CeilingHeight, 9 )
ENDRULE

; SAC 2/4/13 - help to specify Garage EaveZ
RULE ResOtherZn:GrndFlrClgHeight
  DESCRIPTION
    "Ground floor ceiling height"
  DEFAULT
    if( Bottom > -4 .AND. Bottom < 4 )
    then CeilingHeight
    else UNDEFINED
    endif
ENDRULE

; SAC 9/10/19 (MFamProto)
RULE ResOtherZn:CondFloorArea
  DESCRIPTION
    "Ground floor ceiling height"
  DEFAULT
    if( Type = "Conditioned" )
    then Area
    else 0
    endif
ENDRULE

; SAC 04/27/23 (3392)
RULE ResOtherZn:CondFloorAreaWithMult
  DESCRIPTION
    "Ground floor ceiling height"
  DEFAULT
    CondFloorArea * ValidOr( Mult, 1 )
ENDRULE

; SAC 06/28/23 (tic #3479)
RULE ResOtherZn:NewOrAltHVACFloorArea
  DESCRIPTION
    "Ground floor ceiling height"
  DEFAULT
    if( IfValidAnd( HVACSysStatus = "Existing" ) == 0 )
    then CondFloorArea
    else 0
    endif
ENDRULE

; mod to prevent undefined data error - SAC 05/28/21  ; incl. Mult - SAC 04/28/23 (tic #3392)
RULE ResOtherZn:CondVolume
  DESCRIPTION
    "Conditioned volume"
  DEFAULT
    ValidOr( CondFloorArea, 0 ) * ValidOr( CeilingHeight, 9 ) * ValidOr( Mult, 1 )
ENDRULE

RULE ResOtherZn:ClgBlwAtticArea
  DESCRIPTION
    "Sum of areas of children CeilingBelowAttic:Area"
  DEFAULT
    SumChildren( CeilingBelowAttic:Area )
ENDRULE

RULE ResOtherZn:CondClgBlwAtticArea
  DESCRIPTION
    "Sum of areas of children CeilingBelowAttic:Area ONLY FOR Conditioned Zoned"
  DEFAULT
    if( Type = "Conditioned" )
    then ClgBlwAtticArea
    else 0
    endif
ENDRULE

RULE ResOtherZn:HasCondClgBlwAttic
  DESCRIPTION
    "0/1 toggle defining presence of 1 or more child CeilingBelowAttic objects FOR Conditioned
     Zoned"
  DEFAULT
    if( CondClgBlwAtticArea > 0.1 )
    then 1
    else 0
    endif
ENDRULE

RULE ResOtherZn:InterCondZnArea
  DESCRIPTION
    "Int wall or floor area between this and other conditioned zones"
  DEFAULT
    if( Type = "Unconditioned" )
    then 0
    else SumChildren( ResIntWall:InterCondZnArea ) +
         SumChildren( InteriorCeiling:InterCondZnArea ) +
         SumChildren( ResIntFlr:InterCondZnArea ) +
         SumRevRef( ResIntWall:Outside, ResIntWall:InterCondZnArea ) +
         SumRevRef( InteriorCeiling:Outside, InteriorCeiling:InterCondZnArea ) +
         SumRevRef( ResIntFlr:Outside, ResIntFlr:InterCondZnArea )
    endif
ENDRULE

RULE ResOtherZn:PartyEdgeArea
  DESCRIPTION
    "Interior wall/ceiling/floor area that is flagged as 'Party Edge'"
  DEFAULT
    SumChildren( ResIntWall:PartyEdgeArea ) + SumChildren( ResIntFlr:PartyEdgeArea ) +
    SumChildren( InteriorCeiling:PartyEdgeArea )
ENDRULE

RULE ResOtherZn:AtcEdg_CeilBlwAtcArea
  DESCRIPTION
    "Sum of areas of ceiling-below-attic surfaces"
  DEFAULT
    SumChildren( CeilingBelowAttic:Area )
ENDRULE

RULE ResOtherZn:ClVentableClgArea
  DESCRIPTION
    "Sum of child CeilingBelowAttic:ClVentableArea (area adjacent to cool ventable attics)"
  DEFAULT
    SumChildren( CeilingBelowAttic:ClVentableArea )
ENDRULE

RULE ResOtherZn:AtticArea
  DESCRIPTION
    "All attic area (above zone)"
  DEFAULT
    SumChildren( CeilingBelowAttic:Area )
ENDRULE

RULE ResOtherZn:UncondAtticArea
  DESCRIPTION
    "Unconditioned attic area (above zone)"
  DEFAULT
    if ( Type = "Unconditioned" )
    then 0
    else SumChildren( CeilingBelowAttic:UncondAtticArea )
    endif
ENDRULE

// ********** Function ********************************************
RULE ResOtherZn:LaundryArea
  DESCRIPTION
    "Area of zone if SpcFunc laundry related"
  DEFAULT
    if( SpcFunc = "Laundry Area" )
    then ValidOr( Area, 0 )
    else 0
    endif
ENDRULE

RULE ResOtherZn:DryerFuel
  DESCRIPTION
    "Fuel used for clothes dryer(s)"
  DEFAULT
    if(ResProj:Fuel_HaveNatGas = 0)
    then 1
    else 2
    endif
ENDRULE

RULE ResOtherZn:WasherDwellUnits
  DESCRIPTION
    "Num of DwellUnits that reference this OtherZone via DwellUnit:WasherZoneRef"
  DEFAULT
    if( LaundryArea < 0.5 )
    then 0
    else SumRevRef( DwellUnit:WasherZoneRef, DwellUnit:HaveWasher )
    endif
ENDRULE

   ; prevent SpaceFunctionData:FuncSchGrp lookup for 'Unoccupied...' SpcFuncs - SAC 05/28/21
   ; revised to hardwire use of ResidentialCommon schedules for several SpcFuncs - SAC 04/06/22
RULE ResOtherZn:FuncSchGrp
  DESCRIPTION
    "FunctionScheduleGroup"
  DEFAULT
    if( SpcFunc = "- specify -" .OR. 
        SpcFunc = "Unoccupied-Exclude from Gross Floor Area" .OR.
        SpcFunc = "Unoccupied-Include in Gross Floor Area" .OR.
        SpcFunc = "Corridor Area" .OR.
        SpcFunc = "Lobby, Main Entry" .OR.
        SpcFunc = "Restrooms" .OR.
        SpcFunc = "Stairwell" .OR.
        SpcFunc = "All other" .OR.
        SpcFunc = "Aging Eye/Low-vision (Corridor Area)" .OR.
        SpcFunc = "Aging Eye/Low-vision (Lobby, Main Entry)" .OR.
        SpcFunc = "Aging Eye/Low-vision (Restroom)" .OR.
        SpcFunc = "Aging Eye/Low-vision (Stairwell)" )
    then "ResidentialCommon"
    else SpaceFunctionData:FuncSchGrp( "FuncType", SpcFunc )
    endif
ENDRULE

RULE ResOtherZn:IsGarage
  DESCRIPTION
    "flag indicating this OtherZone is a Garage (for AirNet)"
  DEFAULT
    if( SpcFunc = "Parking Garage Area (Daylight Adaptation Zones)" .OR.
        SpcFunc = "Parking Garage Area (Dedicated Ramps)" .OR.
        SpcFunc = "Parking Garage Area (Parking Zone)" .OR.
        SpcFunc = "Parking Garage Area (Parking Zone and Ramps)" .OR.
        SpcFunc = "Parking Garage Area (Daylight Adaptation Zones)" )
    then 1
    else 0
    endif
ENDRULE

// ********** HVAC ********************************************
RULE ResOtherZn:ActiveHVACSystem
  DESCRIPTION
    "Assigned HVAC system, regardless of new/altered/existing type"
  DEFAULT
    if( Type = "Conditioned" .AND. CondType = "Directly" )
    then 
      if( Proj:ResCompOpt != "Newly Constructed" .AND. HVACSysStatus = "Altered" ) 
      then AltHVACSystem
      else if( Proj:ResCompOpt != "Newly Constructed".AND.HVACSysStatus = "Existing" )
      then exHVACSystem
      else ozHVACSystem
      endif endif
    else UNDEFINED
    endif
ENDRULE

RULE ResOtherZn:ServedByAirSys
  DESCRIPTION
    "flag indicating this OtherZone is served by an AirSys"
  DEFAULT
    if( LocalCompAssigned( ActiveHVACSystem ) = ComponentType( "AirSys" ) )
    then 1
    else 0
    endif
ENDRULE

RULE ResOtherZn:HVACSysAssigned
  DESCRIPTION
    "0/1 flag indicating whether cool/heat HVACSystem is assigned"
  DEFAULT
    if( Type = "Conditioned" .AND. LocalCompAssigned( ActiveHVACSystem ) > 0 )
    then 1
    else 0
    endif
ENDRULE

RULE ResOtherZn:HtgSetptSchRef
  DESCRIPTION
    "Hourly heating setpoint schedule"
  SIZING_PROPOSED
    if( FuncSchGrp = "- specify -" )
    then UNDEFINED
    else RuleLibrary( Sch, SpaceFunctionGroups:HtgTstatSchRef( "FuncGroup", FuncSchGrp ) )
    endif
ENDRULE

RULE ResOtherZn:ClgSetptSchRef
  DESCRIPTION
    "Hourly cooling setpoint schedule"
  SIZING_PROPOSED
    if( FuncSchGrp = "- specify -" )
    then UNDEFINED
    else RuleLibrary( Sch, SpaceFunctionGroups:ClgTstatSchRef( "FuncGroup", FuncSchGrp ) )
    endif
ENDRULE

RULE ResOtherZn:IAQCFMFracHRVCompat
  DESCRIPTION
    "fraction of design IAQ CFM at/below % cutoff (above minimum CFM) allowed for HRV"
  DEFAULT
    UNDEFINED
ENDRULE

; inputs for creation of AirSys to serve active ResOtherZn - SAC 05/17/22
RULE ResOtherZn:AirSys_WizDlgDims[1]
  DESCRIPTION
    "dialog dimensions - width"
  DEFAULT
    500
ENDRULE

RULE ResOtherZn:AirSys_WizDlgDims[2]
  DESCRIPTION
    "dialog dimensions - height"
  DEFAULT
    500
ENDRULE

RULE ResOtherZn:AirSys_NameRoot
  DESCRIPTION
    "Root of name for each AirSys equipment component"
  DEFAULT
    substr( Name, 0, 16 )
ENDRULE

// ------------ E+A+A Rules --------------------------
; SAC 9/19/19 (MFamProto)
RULE ResOtherZn:IsEAAAdditionZone
  DESCRIPTION
    "indication that this is an E+A+A Addition Zone"
  DEFAULT
    0
ENDRULE

RULE ResOtherZn:HVACNewCondFloorAreaWithMult
  DESCRIPTION
    "Conditioned floor area with multiplier for assigned new HVAC system"
  DEFAULT
    if( Type = "Conditioned" ) then CondFloorAreaWithMult else 0 endif
ENDRULE

RULE ResOtherZn:HVACAltCondFloorAreaWithMult
  DESCRIPTION
    "Conditioned floor area with multiplier for assigned altered HVAC system"
  DEFAULT
    0 ; if (ResProj:RunScope == 2 .AND. HVACSysStatus == 2) then  CondFloorArea  else  0  endif  }
ENDRULE

RULE ResOtherZn:HVACExCondFloorAreaWithMult
  DESCRIPTION
    "Conditioned floor area with multiplier for assigned existing HVAC system"
  DEFAULT
    0 ; if (ResProj:RunScope == 2 .AND. HVACSysStatus == 1) then  CondFloorArea  else  0  endif  }
ENDRULE

RULE ResOtherZn:HVACExVCondFloorAreaWithMult
  DESCRIPTION
    "Conditioned floor area with multiplier for assigned existing (altered & verified) HVAC system"
  DEFAULT
    0 ; if (ResProj:RunScope == 2 .AND. HVACSysStatus == 2 .AND. HVACSysVerified) then  CondFloorArea  else  0  endif  }
ENDRULE

// ------------ Description of zone for display in UI Tree --------------------------
RULE ResOtherZn:TreeDescrip
  DESCRIPTION
    "Description of zone for display in UI Tree"
  DEFAULT
    if( IfValidAnd( Area > 0.1 ) )
    then if( Type = "Conditioned" )
         then Format( "%s, %s %s, %s ft2", SpcFunc, CondType, Type, FltToStr( Area ) )
         else Format( "%s, %s, %s ft2", SpcFunc, Type, FltToStr( Area ) )
         endif
    else if( Type = "Conditioned" )
         then Format( "%s, %s %s, no floor area specified", SpcFunc, CondType, EnumString( Type ) )
         else Format( "%s, %s, no floor area specified", SpcFunc, Type )
         endif  
    endif
ENDRULE

// ------------ CSE simulation settings --------------------------
RULE ResOtherZn:ZnModel
  DESCRIPTION
    "Zone model"
  DEFAULT
    switch( Local( Type ) )
      case "Conditioned"    : "CZM" ; Conditioned zone model. Forward-difference, short time step methods are used.
      case "Unconditioned"  : "UZM" ; Unconditioned zone model. Identical to CZM except heating and cooling are not supported.
      default               : "CZM"
    endswitch
ENDRULE

RULE ResOtherZn:InfAC
  DESCRIPTION
    "Zone infiltration air changes per hour"
  DEFAULT
    0 ; 0s defaults of old infiltration models
ENDRULE

RULE ResOtherZn:InfELA
  DESCRIPTION
    "Zone effective leakage area"
  DEFAULT
    0 ; 0s defaults of old infiltration models
ENDRULE

RULE ResOtherZn:InfShld
  DESCRIPTION
    "Zone local shielding class, used in derivation of local wind speed for ELA infiltration model,
     wind-driven AirNet leakage, and exterior surface coefficients."
  DEFAULT
    4 ; heavy shielding, obstructions around most of the perimeter
ENDRULE

RULE ResOtherZn:znCAir
  DESCRIPTION
    "Zone 'air' heat capacity: represents heat capacity of air, furniture, 'light' walls, and
     everything in zone except nonQUICK surfaces"
  DEFAULT
    if( Type = "Conditioned" )
    then ValidOr( CondFloorArea, 0 ) * 2
    else UNDEFINED endif
ENDRULE
