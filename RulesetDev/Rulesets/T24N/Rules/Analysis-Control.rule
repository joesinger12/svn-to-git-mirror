// Analysis - Control
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

// No direct correlation of ACM topics - just miscellaneous rules pertaining to
// compliance analysis
// 

//    NewComplete
//    NewEnvelope
//    NewEnvelopeAndLighting
//    NewEnvelopeAndPartialLighting
//    NewEnvelopeAndMechanical
//    NewMechanical
//    NewMechanicalAndLighting
//    NewMechanicalAndPartialLighting
//    ExistingAlteration
//    ExistingAdditionAndAlteration
//    AdditionComplete
//    AdditionEnvelope
//    AdditionEnvelopeAndLighting
//    AdditionEnvelopeAndPartialLighting
//    AdditionEnvelopeAndMechanical
//    AdditionMechanical
//    AdditionMechanicalAndLighting
//    AdditionMechanicalAndPartialLighting


// -------------- Simulation Flag ----------------------------------------------
RULE Proj:SimFlag
  DESCRIPTION
     "Whether or not to perform simulation (transform-specific)."  
  DEFAULT
    1
  SIZING_PROPOSED
    if( CreateBaseHVAC > 0 .OR. SumAll( ThrmlZn:AddClgToProp ) > 0 )
    then 1
    else 0
    endif
  SIZING_BASELINE
    if( BaseHVACSameAsPropHVAC > 0 .AND.
        HasBaseHVAC = 0 )
    then 0
    else 1
    endif
  ANNUAL_PROPOSED 
    1
  ANNUAL_BASELINE
    1
ENDRULE


// -------------- Transform Object EXCLUDE control -----------------------------
// Set flag to determine when USER/SIZING_PROPOSED model objects are excluded, 
// i.e. not copied to the SIZING_BASELINE transform.
// Value for Proj:BaseHVACMapForAlt is determined in separate SIZING_PROPOSED 
// rule based on sum capacity of components that are altered.

// AirSystems
RULE AirSys:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed AirSys objects (and thier children) are
     excluded from copy to a subsequent transform."
  HELP
    "If value is 1, the object is excluded when copying to the subsequent transform.
     Proposed HVAC is excluded from copy when the baseline HVAC system is specified
     by the NACM system map."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // Exclude all user-defined HVAC systems for partial compliance types  
         // that do not include permitting of mechanical systems or existing    
         // systems if modeling additions.                                      
      1
    else
    if( IfValidAnd( IsFuture > 0 ) )
    then // Exclude AirSys if all the zones it serves are HasUnknownHVAC = 1
      1    
    else 0
    endif endif
  SIZING_PROPOSED : T24N
    if( IsExhSys > 0 )
    then // Non-healthcare exhaust systems are ignored by the translator since exhaust for simulation
         // is defined at ThrmlZn. However, exclude them in copy from proposed 
         // to baseline anyways
      if( IfValidAnd( IsHlthCareSys > 0 ) )
      then 0 // Systems serving healthcare spaces are the same in proposed and baseline
      else 1
      endif
    else
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // This is a Partial Envelope analysis, and the baseline = proposed 
         // (if not an existing system) so do not exclude in copy from zp to zb
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. ( IsExisting > 0 .AND. IsAltered = 0 ) )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting but not Altered, so do not exclude in copy from zp to zb
      0
    else
    if( IfValidAnd( IsCondgSys = 0 ) )
    then // Non-exhaust systems serving Unconditioned spaces are the same in proposed and baseline
      0
    else
    if( IfValidAnd( IsHlthCareSys > 0 ) )
    then // Systems serving healthcare spaces are the same in proposed and baseline
      0
    else
    if( SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResVentEquipChkReq ) > 0 .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResVentEquipIsAccessible ) = 0 )
    then // System is unitary ventilation system and is not accessible, Standard = Proposed
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif endif
    endif endif
  SIZING_PROPOSED : S901G ECBC
    if( IsExhSys > 0 )
    then // Exhaust systems are ignored by the translator since exhaust for simulation
         // is defined at ThrmlZn. However, exclude them in copy from proposed 
         // to baseline anyways
      1
    else
    if( Proj:IsNoMech > 0 )
    then // This is a Partial Envelope analysis, and the baseline = proposed 
         // so do not exclude in copy from zp to zb
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. IsExisting > 0 )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting so do not exclude in copy from zp to zb
      0
    else
    if( NotEnclosedSys > 0 .OR. IfValidAnd( IsCondgSys = 0 ) )
    then // Systems serving NotEnclosed or Unconditioned spaces are the same in proposed and baseline
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif endif
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE
// TrmlUnits
RULE TrmlUnit:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed TrmlUnit objects (and thier children) are
     excluded from copy to a subsequent transform."
  HELP
    "If value is 1, the object is excluded when copying to the subsequent transform.
     Proposed terminal units are excluded from copy when the zone is defined 
     as 'HasUnknownHVAC'."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) .OR. AirSys:ExcludeFromSizing > 0 )
    then // Exclude all user-defined HVAC systems for partial compliance types  
         // that do not include permitting of mechanical systems or existing    
         // systems if modeling additions.                                      
      1
    else
    if( LocalCompAssigned( ZnServedRef ) )
    then
      if( ZnServedRef:HasUnknownHVAC > 0 )
      then 1    
      else 0
      endif
    else 0
    endif endif
  SIZING_PROPOSED
    if( AirSys:ExcludeFromSizing > 0 )
    then                                     
      1
    else UNCHANGED
    endif
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE

// ZoneSystems
RULE ZnSys:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed ZnSys objects (and thier children) are
     excluded from copy to a subsequent transform."
  HELP
    "If value is 1, the object is excluded when copying to the subsequent transform.
     Proposed HVAC is excluded from copy when the baseline HVAC system is specified
     by the NACM system map."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // Exclude all user-defined HVAC systems for partial compliance types  
         // that do not include permitting of mechanical systems or existing    
         // systems if modeling additions.                                      
      1
    else
    if( IfValidAnd( IsFuture > 0 ) )
    then // Exclude ZnSys if the zone it serves is HasUnknownHVAC = 1
      1    
    else 0
    endif endif
  SIZING_PROPOSED : T24N
    if( IsExhSys > 0 )
    then // Exhaust systems are ignored by the translator since exhaust for simulation
         // is defined at ThrmlZn. However, exclude them in copy from proposed 
         // to baseline anyways
      if( IfValidAnd( IsHlthCareSys > 0 ) )
      then 0 // Systems serving healthcare spaces are the same in proposed and baseline
      else 1
      endif
    else
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // This is a Partial Envelope analysis, and the baseline = proposed      
         // (if not an existing system) so do not exclude in copy from zp to zb   
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. ( IsExisting > 0 .AND. IsAltered = 0 ) )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting but not Altered, so do not exclude in copy from zp to zb
      0
    else
    if( IfValidAnd( IsCondgSys = 0 ) )
    then // Systems serving Unconditioned spaces are the same in proposed and baseline
      0
    else
    if( IfValidAnd( IsHlthCareSys > 0 ) )
    then // Systems serving healthcare saces are the same in proposed and baseline
      0
    else
    if( SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResVentEquipChkReq ) > 0 .AND.
        SumRevRef( ThrmlZn:VentSysRef, ThrmlZn:ResVentEquipIsAccessible ) = 0 )
    then // System is unitary ventilation system and is not accessible, Standard = Proposed
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif endif
    endif endif
  SIZING_PROPOSED : S901G ECBC
    if( IsExhSys > 0 )
    then // Exhaust systems are ignored by the translator since exhaust for simulation
         // is defined at ThrmlZn. However, exclude them in copy from proposed 
         // to baseline anyways
      1
    else
    if( Proj:IsNoMech > 0 )
    then // This is a Partial Envelope analysis, and the baseline = proposed 
         // so do not exclude in copy from zp to zb
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. IsExisting > 0 )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting so do not exclude in copy from zp to zb
      0
    else
    if( NotEnclosedSys > 0 .OR. IfValidAnd( IsCondgSys = 0 ) )
    then // Systems serving NotEnclosed or Unconditioned spaces are the same in proposed and baseline
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif endif
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE

RULE VRFSys:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed VRFSys objects (and thier children) are
     excluded from copy to a subsequent transform."
  HELP
    "If value is 1, the object is excluded when copying to the subsequent transform.
     Proposed HVAC is excluded from copy when the baseline HVAC system is specified
     by the NACM system map."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // Exclude all user-defined HVAC systems for partial compliance types  
         // that do not include permitting of mechanical systems or existing    
         // systems if modeling additions.                                      
      1
    else
    if( SumRevRef( ZnSys:VRFSysRef, ZnSys:ExcludeFromSizing ) = CountRefs( ZnSys:VRFSysRef ) )
    then // Exclude VRFSys if all the ZnSys that reference it are excluded
      1    
    else 0
    endif endif
  SIZING_PROPOSED : T24N
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // This is a Partial Envelope analysis, and the baseline = proposed      
         // (if not an existing system) so do not exclude in copy from zp to zb   
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. ( IsExisting > 0 .AND. IsAltered = 0 ) )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting but not Altered, so do not exclude in copy from zp to zb
      0
    else
    if( IfValidAnd( IsCondgSys = 0 ) )
    then // Systems serving Unconditioned spaces are the same in proposed and baseline
      0
    else
    if( IfValidAnd( IsHlthCareSys > 0 ) )
    then // Systems serving healthcare saces are the same in proposed and baseline
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif endif
  SIZING_PROPOSED : S901G ECBC
    if( Proj:IsNewEnv > 0 .AND. Proj:IsNewMech = 0 )
    then // This is a Partial Envelope analysis, and the baseline = proposed 
         // so do not exclude in copy from zp to zb
      0
    else
    if( Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else 
    if( Proj:IsAddOrAlt > 0 .AND. IsExisting > 0 )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting so do not exclude in copy from zp to zb
      0
    else
    if( NotEnclosedSys > 0 .OR. IsCondgSys = 0 )
    then // Systems serving NotEnclosed or Unconditioned spaces are the same in proposed and baseline
      0
    else // Exclude in copy from zp to zb
      1
    endif endif endif endif
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE

// Coils (for determining Exclude for FluidSys)
RULE NEW CoilHtg:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to coils for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    if( IfValidAnd( ParentComp = "AirSys" ) .OR. IfValidAnd( ParentComp = "TrmlUnit" ) )
    then AirSys:ExcludeFromSizing
    else if( IfValidAnd( ParentComp = "ZnSys" ) )
    then ZnSys:ExcludeFromSizing
    else 1
    endif endif
ENDRULE
RULE NEW CoilClg:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to coils for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    if( IfValidAnd( ParentComp = "AirSys" ) .OR. IfValidAnd( ParentComp = "TrmlUnit" ) )
    then AirSys:ExcludeFromSizing
    else if( IfValidAnd( ParentComp = "ZnSys" ) )
    then ZnSys:ExcludeFromSizing
    else 1
    endif endif
ENDRULE
RULE NEW FluidSeg:CoilsNotExcluded
  DATATYPE
    Integer
  LONGFORM
    CoilsNotExcluded
  DESCRIPTION
    "A flag indicating if any of the coils referencing the FluidSeg are not
     excluded from sizing, i.e. ExcludeFromSizing = 0, and therefore the FluidSys
     should not be excluded."
  INPUTCLASS 
    NotInput
  SIZING
    if( ValidOr( MinRevRef( CoilHtg:FluidSegInRef, CoilHtg:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( CoilClg:FluidSegInRef, CoilClg:ExcludeFromSizing ), 999 ) = 0 ) 
    then 1
    else 0
    endif
ENDRULE


RULE FluidSys:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed FluidSys objects (and thier children) are
     excluded from copy to a subsequent transform."
  HELP
    "Proposed HVAC is excluded from copy when the baseline HVAC system is specified
     by the NACM system map."
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "ServiceHotWater" )
    then UNCHANGED // See rule below
    else
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) )
    then // Exclude all user-defined HVAC systems for partial compliance types 
         // that do not include permitting of mechanical systems or existing
         // systems if modeling additions.
      1
    else 0
    endif endif
  SIZING_PROPOSED
    if( Type = "ServiceHotWater" )
    then UNCHANGED // See rule below
    else
    if( Type != "ServiceHotWater" .AND. Proj:BaseHVACSameAsPropHVAC > 0 ) 
    then // This is an Addition and/or Alteration analysis with no new 
         // non-ChW/HW/Steam coil capacity so do not exclude components
         // in copy from zp to zb
      0
    else
    if( Proj:IsNoMech > 0 .OR. ( Proj:IsNoAddMech > 0 .AND. IsNew > 0 ) .OR.
         SumChildren( FluidSeg:CoilsNotExcluded ) > 0 )
    then // This is a Partial Envelope analysis, and the baseline = proposed 
         // (if not an existing system) so do not exclude in copy from zp to zb - OR-
         // the FluidSys serves coils that are not excluded in copy, and therefore
         // the FluidSys should not be excluded.
      0
    else
    if( Proj:IsAddOrAlt > 0 )
    then // Proj:CompType is and Addition* or AdditionAlteration 
      if( Type = "ServiceHotWater" .AND. Proj:IsNewSHWFluidSys > 0 )
      then UNCHANGED // See rule below
      else
      if( Type != "ServiceHotWater" .AND. Proj:BaseHVACSameAsPropHVAC > 0 ) 
      then // This is an Addition and/or Alteration analysis with no new 
           // non-ChW/HW/Steam coil capacity so do not exclude components
           // in copy from zp to zb
        0
      else
      if( ( IsAltered > 0 .OR. IsExisting > 0 ) )
      then // The FluidSys is not new, but is Altered or Existing
        if( SumChildren( FluidSeg:NumNewPriObj ) + SumChildren( FluidSeg:NumNewCoilObj )  = 0 ) 
        then // FluidSys has no new primary equipment and does not serve any 'IsNew' systems 
             // so system is needed in baseline
          0
        else
        if( SumChildren( FluidSeg:NumNewPriObj ) = 0 .AND. SumChildren( FluidSeg:CoilsNotExcluded ) = 0 ) 
        then // FluidSys has no new primary equipment, but all coils that reference the system are excluded, 
             // so system is not needed in baseline
          1
        else 
        if( SumChildren( FluidSeg:NumNewPriObj ) > 0 )
        then // FluidSys has new primary equipment. Therefore check
             // to see if Add/Alt system map criteria for baseline = proposed
          if( ( Bldg:TotClgCap > 0 .AND. Bldg:DeltaClgCapAltAddRat = 0 ) .OR.
              ( Bldg:TotClgCap = 0 .AND. Bldg:DeltaHtgCapAltAddRat = 0 ) )
          then 0 // Baseline = proposed if IsAltered and change in building
                 // cooling capacity is 0 or is a heating only building with 
                 // no new heating capacity.     
          else 1
          endif
        else 0
        endif endif endif
      else 1 // Exclude in copy from zp to zb
      endif endif endif
    else 1
    endif endif endif endif
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE

// Issue 2065: Add additional criteria to determine if when a FluidSys should or shouldn't
// be excluded.
RULE NEW Blr:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to primary equipment for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    FluidSys:ExcludeFromSizing
ENDRULE
RULE NEW Chlr:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to primary equipment for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    FluidSys:ExcludeFromSizing
ENDRULE
RULE NEW HtRej:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to primary equipment for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    FluidSys:ExcludeFromSizing
ENDRULE
RULE NEW ThrmlEngyStor:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to primary equipment for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    FluidSys:ExcludeFromSizing
ENDRULE
RULE NEW HX:ExcludeFromSizing
  DATATYPE
    Integer
  DESCRIPTION
    "Echo ExcludeFromSizing to primary equipment for determining when FluidSys should be excluded."
  INPUTCLASS
    NotInput
  SIZING
    FluidSys:ExcludeFromSizing
ENDRULE
RULE NEW FluidSeg:PriEquipNotExcluded
  DATATYPE
    Integer
  LONGFORM
    PrimaryEquipmentNotExcluded
  DESCRIPTION
    "A flag indicating if any of the primary equipment referencing the FluidSeg are not
     excluded from sizing, i.e. ExcludeFromSizing = 0, and therefore the FluidSys
     should not be excluded."
  INPUTCLASS 
    NotInput
  SIZING
    if( ValidOr( MinRevRef( Blr:FluidSegInRef, Blr:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( Chlr:EvapFluidSegInRef, Chlr:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( Chlr:CndsrFluidSegInRef, Chlr:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( Chlr:GenFluidSegInRef, Chlr:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( HtRej:FluidSegInRef, HtRej:ExcludeFromSizing ), 999 ) = 0 .OR.        
        ValidOr( MinRevRef( ThrmlEngyStor:ChrgFluidSegInRef, ThrmlEngyStor:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( ThrmlEngyStor:DischrgFluidSegInRef, ThrmlEngyStor:ExcludeFromSizing ), 999 ) = 0 .OR.
        ValidOr( MinRevRef( HX:CHWFluidSegInRef, HX:ExcludeFromSizing ), 999 ) = 0 ) 
    then 1
    else 0
    endif
ENDRULE
// Update FluidSys:ExcludeFromSizing to be 0 if any of the primary equipment served by child FluidSegs
// serve equipment that won't be excluded, and therefore should stay in model.
RULE FluidSys:ExcludeFromSizing
  SIZING
    if( SumChildren( FluidSeg:PriEquipNotExcluded ) > 0 )
    then 0
    else UNCHANGED
    endif
ENDRULE
    
  


//--------------------------------------------------------------------------------       
// Rule for ServiceHotWater systems
RULE FluidSys:ExcludeFromSizing
  DEFAULT
    if( Type = "ServiceHotWater" )
    then
      if( Proj:ExcptCondWtrHtr = "Yes" )
      then 1
      else 0
      endif
    else UNCHANGED
    endif
  SIZING_PROPOSED
    if( Type = "ServiceHotWater" )
    then
      if( Proj:IsAddOrAlt > 0 .AND. Proj:IsNewSHWFluidSys = 0 .AND. Proj:ExcptCondWtrHtr = "No" )
      then 0
      else 1
      endif
    else if( Proj:IsAddOrAlt > 0 )
    then // Proj:CompType is Addition* or AdditionAlteration 
      if( Type = "ServiceHotWater" .AND. Proj:IsNewSHWFluidSys > 0 )
      then 1
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
ENDRULE


RULE ResDHWSys:SimStdEngyUse
  DESCRIPTION
    "A flag that indicates if standard design DHW energy use should rely on the
     CEC DHW engine's internal std design mechanism (0) or if the DHW system data
     should be simulated as if it were a proposed system (1)"
  HELP
    "A flag that indicates if standard design DHW energy use should rely on the
     CEC DHW engine's internal std design mechanism (0) or if the DHW system data
     should be simulated as if it were a proposed system (1)"
  INPUTCLASS
    NotInput
  DEFAULT : T24N_2016
    1
  DEFAULT : T24N
    1    
  DEFAULT
    // Simulate Std design same as proposed if ComplianceType -> IsNewEnvelope
    // (and there is no new mechanical)
//  if( Proj:IsNewEnv > 0 .AND. Proj:IsNewMech = 0 )
    if( Proj:ExcptCondWtrHtr = "No" )
    then 1
    else 0
    endif
  SIZING_PROPOSED : T24N_2016
    1
  SIZING_PROPOSED : T24N
    1    
  SIZING_PROPOSED
    if (IfValidAnd( IsBaseSys > 0 ))
    then  0
    else  u:SimStdEngyUse
    endif
  SIZING_BASELINE : T24N_2016
    1
  SIZING_BASELINE : T24N
    1
  SIZING_BASELINE
//  Removed because Base = Prop for only the next condition
//  if( Proj:IsNewEnv = 1 .AND. Proj:IsNewMech = 0 )
//  then // This is a Partial Envelope analysis, and the baseline = proposed 
//    1
//  else 
//  if( Proj:IsAddOrAlt > 0 .AND. IsExistingDHW > 0 .AND. Proj:IsNewDHWFluidSys = 0)
    if( Proj:IsAddOrAlt > 0 .AND. Proj:IsNewDHWFluidSys = 0 .AND. Proj:ExcptCondWtrHtr = "No" )
    then // This is an Addition and/or Alteration analysis, and baseline = proposed 
         // if IsExisting so simulate standard as we do proposed
      1
    else // Use DHW engine to compute std design energy use based on proposed inputs
      0
    endif
//    endif
  ANNUAL
    z:SimStdEngyUse
ENDRULE


//RULE ResDHWSys:ExcludeFromSizing
//  DESCRIPTION
//    "A flag that indicates if proposed ResDHWSystem objects (and their children) are
//     excluded from copy to a subsequent transform."
//  HELP
//    ""
//  INPUTCLASS
//    NotInput
//  DEFAULT
//    // For non-central residential DHW systems, do not copy to baseline.  
//    // For central residential DHW systems, copy to baseline
//    if( CentralSys = 0 )
//    then 1
//    else 0
//    endif
//  SIZING_PROPOSED : T24N_2013
//    u:ExcludeFromSizing
////    1        // this is correct, but need to ensure rules will create correct configuration of baseline.
//  SIZING_PROPOSED
//    u:ExcludeFromSizing
//  SIZING_BASELINE
//    0
//  ANNUAL
//    0
//ENDRULE
//

RULE ResDHWSys:ExcludeFromSizing
  DESCRIPTION
    "A flag that indicates if proposed ResDHWSystem objects (and their children) are
     excluded from copy to a subsequent transform."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    if( Proj:ExcptCondWtrHtr = "Yes" ) // When water heating is excluded remove the proposed system
    then 1   
    else 0   
    endif
  SIZING_PROPOSED
    0 
  SIZING_BASELINE
    0
  ANNUAL
    0
ENDRULE

                           

// ----------------------------------------------------------------
RULE NEW Proj:StdsYr
  DATATYPE
    Integer
  LONGFORM
    StandardsYear
  INPUTCLASS
    Notinput
  DESCRIPTION
    "Year of the Title 24 Standard"  
  DEFAULT : T24N_2013
    2013
  DEFAULT : T24N_2016
    2016
  DEFAULT : T24N
    2019
ENDRULE


// ----------------------------------------------------------------
// Define TypeSim for SDD -> OS translation/simulation
RULE AirSys:TypeSim
  SIZING
    switch( Type ) 
      case "SPVAC"   : "SZAC"   
      case "SPVHP"   : "SZHP"
      case "DOASCV"  : "SZAC"
      case "DOASVAV" : "SZVAVAC" 
      default        : Type
    endswitch
  ANNUAL
    switch( Type ) 
      case "SPVAC"   : "SZAC"   
      case "SPVHP"   : "SZHP"
      case "DOASCV"  : "SZAC"
      case "DOASVAV" : "SZVAVAC" 
      default        : Type
    endswitch
ENDRULE

RULE ZnSys:TypeSim
  SIZING
    switch( Type )
      case "SZAC"            : "PTAC"
      case "SZHP"            : "PTHP"   
      case "SPVAC"           : "PTAC"   
      case "SPVHP"           : "PTHP"
      case "MiniSplitAC"     : "PTAC"
      case "MiniSplitHP"     : "PTHP"
      case "Radiant"         : "Radiant"
      case "PassiveBeam"     : "FPFC"
      case "Furnace"         : "PTAC"
      case "FPFC"            : if( IfValidAnd( CoilClgRef:IsDummy > 0 ) )
                               then "PTAC" // Switch to PTAC for simulation
                               else Type
                               endif
      case "VentilationOnly" : "Exhaust"
      default                : Type
    endswitch
  ANNUAL
    switch( Type )
      case "SZAC"            : "PTAC"
      case "SZHP"            : "PTHP"   
      case "SPVAC"           : "PTAC"   
      case "SPVHP"           : "PTHP"
      case "MiniSplitAC"     : "PTAC"
      case "MiniSplitHP"     : "PTHP"
      case "Radiant"         : "Radiant"
      case "PassiveBeam"     : "FPFC"
      case "Furnace"         : "PTAC"
      case "FPFC"            : if( IfValidAnd( CoilClgRef:IsDummy > 0 ) )
                               then "PTAC" // Switch to PTAC for simulation
                               else Type
                               endif
      case "VentilationOnly" : "Exhaust"
      default                : Type
    endswitch
ENDRULE

RULE TrmlUnit:TypeSim
  DESCRIPTION
    "The simulates system type for SDD -> OS reverse translator."
  HELP
    "The 'Type' is an important description that is relevant to rules,
     reporting, and how it is represented in the simulation. To separate
     the reporting/rules from the simulatiuon, TypeSim is used to 
     describe how it shall be represented in the simulation. For TerminalUnits,
     translation is always to the set of AirTerminal types that are supported by E+."
  SIZING
    if( IfValidAnd( IsActiveBeam > 0 ) )
    then UNCHANGED
    else Type
    endif
  ANNUAL
    if( IfValidAnd( IsActiveBeam > 0 ) )
    then UNCHANGED
    else Type
    endif
ENDRULE

RULE WtrHtr:TypeSim
  DESCRIPTION
    "The simulates system type for SDD -> OS reverse translator."
  HELP
    "The 'Type' is an important description that is relevant to rules,
     reporting, and how it is represented in the simulation. To separate
     the reporting/rules from the simulatiuon, TypeSim is used to 
     describe how it shall be represented in the simulation."   
  SIZING
    switch( Type ) 
      case "HeatPumpPackaged" : "HeatPumpSplit"   
      case "HeatPumpSplit"    : "HeatPumpSplit"
      default                 : "Conventional"
    endswitch
  ANNUAL
    switch( Type ) 
      case "HeatPumpPackaged" : "HeatPumpSplit"   
      case "HeatPumpSplit"    : "HeatPumpSplit"
      default                 : "Conventional"
    endswitch
ENDRULE

// SAC 11/3/19 - replaced by Proj:ElecDemMultTableName[1-10]
// // ---------------------------------------------------------------- SAC 10/8/16
// // Name of table:column containing hourly electric demand multipliers
// // note: demand calcs enabled only for runs where this is defined
// RULE NEW Proj:HrlyElecDemMultTableAndColumnName
//   DATATYPE 
//     String
//   LONGFORM
//     HourlyElectricDemandMultiplierTableAndColumnName
//   DESCRIPTION
//     "Hourly electric demand multiplier table name"
//   INPUTCLASS
//     Prescribed
//   DEFAULT 
//     if (IfValidAnd( EngyCodeYearNum >= 2019 ))
//     then  "CANonResDemandMultipliers:DemMult"
//     else  UNDEFINED
//     endif
// ENDRULE


// ----------------------------------------------------------------
// Add bypass for UMLH limits
RULE ThrmlZn:BypassClgUMLHLimit
  DESCRIPTION
    "A flag that indicates the limit on cooling unmet load hours
     for the zone is bypassed."
  INPUTCLASS
    NotInput
  DEFAULT
    0
  SIZING
    if( IsCond = 0 )
    then 1
    else
    if( BaseSysNum > 0 .OR. // Covers baseline and HasUnknownHVAC cases
        ( IsAddOrAlt > 0 .AND. IsExistingHVAC > 0 ) .OR. // HVACStatus = 'Existing'
        AddClgToProp > 0 .OR. // Proposed has no cooling, it is added by rules
        IsHlthCare > 0 ) // Is a healthcare zone
    then 1
    else BypassClgUMLHLimit
    endif endif
ENDRULE

RULE ThrmlZn:BypassHtgUMLHLimit
  DESCRIPTION
    "A flag that indicates the limit on heating unmet load hours
     for the zone is bypassed."
  INPUTCLASS
    NotInput
  DEFAULT
    0
  SIZING
    if( IsCond = 0 )
    then 1
    else
    if( BaseSysNum > 0 .OR. // Covers baseline and HasUnknownHVAC cases
        ( IsAddOrAlt > 0 .AND. IsExistingHVAC > 0 ) .OR. // HVACStatus = 'Existing'
        IsHlthCare > 0 ) // Is a healthcare zone
    then 1
    else BypassHtgUMLHLimit
    endif endif
ENDRULE
