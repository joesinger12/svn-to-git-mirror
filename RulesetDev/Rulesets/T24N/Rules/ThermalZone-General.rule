// ThermalZone - General Information
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  This rule file addresses the following building descriptors:
//  Section 5.3.1 Thermal Zones - General Information 
//      Thermal Zone Name
//      Thermal Zone Description 
//      Thermal Zone Type
//      System Name
//      Floor Area

       

// ---------- Section 5.3.1 - Thermal Zones - General Information --------------


// ********** Thermal Zone Name ************************************************
RULE ThrmlZn:Name
  DESCRIPTION
    "A unique identifier for the ThermalZone"  
  HELP
    "A unique identifier for the ThermalZone made up of 50 or fewer 
     alphanumeric characters."  
  REFERENCE 
    NACM Section 5.3.1    
  INPUTCLASS
    Required
  // Transformation definitions initialized to be the same as user input.
ENDRULE


// ********** Thermal Zone Description *****************************************
RULE ThrmlZn:Desc
  DESCRIPTION
    "A unique identifier for the ThermalZone"  
  HELP
    "A brief description of the ThermalZone that identifies the spaces 
     that make up the ThermalZone or other descriptive information. 
     The description should tie the ThermalZone to the building plans."  
  REFERENCE 
    NACM Section 5.3.1    
  INPUTCLASS
    Optional
  // Transformation definitions initialized to be the same as user input.
ENDRULE


// ********** Thermal Zone Type ************************************************
// First sum up space Area and areas by CondgType for CHECKCODE rules
RULE NEW ThrmlZn:Area
  DATATYPE
    Float
  LONGFORM
    Area
  DESCRIPTION
    "The total area of the ThermalZone based on Spc:Area" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:Area ) 
ENDRULE
RULE NEW ThrmlZn:DirectCondArea
  DATATYPE
    Float
  LONGFORM
    DirectConditionedArea
  DESCRIPTION
    "The area of the ThermalZone that is Spc:CondgType = 'DirectlyConditioned'" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:DirectCondArea ) 
ENDRULE
RULE NEW ThrmlZn:IndirectCondArea
  DATATYPE
    Float
  LONGFORM
    IndirectConditionedArea
  DESCRIPTION
    "The area of the ThermalZone that is Spc:CondgType = 'IndirectlyConditioned'" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:IndirectCondArea ) 
ENDRULE
RULE NEW ThrmlZn:UncondArea
  DATATYPE
    Float
  LONGFORM
    UnconditionedArea
  DESCRIPTION
    "The area of the ThermalZone that is Spc:CondgType = 'Unconditioned'" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:UncondArea ) 
ENDRULE
RULE NEW ThrmlZn:PlenumArea
  DATATYPE
    Float
  LONGFORM
    PlenumArea
  DESCRIPTION
    "The area of the ThermalZone that is Spc:CondgType = 'Plenum'" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:PlenumArea ) 
ENDRULE
RULE NEW ThrmlZn:UnoccupiedArea
  DATATYPE
    Float
  LONGFORM
    UnoccupiedArea
  DESCRIPTION
    "The area of the ThermalZone that is 'Unoccupied'" 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:UnoccupiedArea ) 
ENDRULE
// Then define rule
RULE ThrmlZn:Type
  DESCRIPTION
    "The conditioning type designation for the ThermalZone"  
  HELP
    "Designation of the ThermalZone as directly conditioned space, 
     indirectly conditioned space (i.e., conditioned only by passive heating 
     or cooling from an adjacent ThermalZone), or plenum (i.e., unoccupied 
     but partially conditioned as a consequence of its role as a path for 
     returning air)."  
  REFERENCE 
    NACM Section 5.3.1    
  INPUTCLASS
    Default
  OPTION         
    Conditioned
    Unconditioned
    Plenum
  DEFAULT 
    if( UncondArea > 0 )
    then "Unconditioned"
    else if( PlenumArea > 0 )
    then "Plenum"
    else "Conditioned"
    endif endif
 CHECKCODE
   // Checks for various invalid configurations
   if( Type = "Conditioned" .AND. DirectCondArea > 0 )
   then // Zone is Conditioned and includes DirectlyConditioned space
     if( UncondArea > 0 )
     then // Conditioned ThrmlZn includes Unconditioned space area
       PostError("ThermalZone '%s' combines 'Unconditioned' and
                  'DirectlyConditioned' space area; this is not permitted.",
                 Name )
     else if( PlenumArea > 0 )
     then // Conditioned ThrmlZn includes Plenum space area
       PostError("ThermalZone '%s' combines 'Plenum' and
                  'DirectlyConditioned' space area; this is not permitted.",
                 Name)
     else UNCHANGED
     endif endif
   else if( Type = "Unconditioned" )
     then
     if( HasHtg .OR. HasClg )
     then // Unconditioned ThrmlZn is served by a pri. or vent. system with htg or clg
       PostError("ThermalZone '%s' is 'Unconditioned' but is served by a Primary
                  or Ventilation system with heating and/or cooling; this is not
                  currently permitted.",
                 Name)    
     else 
     if( DirectCondArea > 0 )
     then // Unconditioned ThrmlZn includes Conditioned space area
       PostError("ThermalZone '%s' combines 'Unconditioned' and
                  'DirectlyConditioned' space area; this is not permitted.",
                 Name)
     else if( PlenumArea > 0 )
     then // Conditioned ThrmlZn includes Plenum space area
       PostError("ThermalZone '%s' combines 'Plenum' and
                  'Unconditioned' space area; this is not permitted.",
                 Name)
     else if( UncondArea > 0 .AND. IndirectCondArea > 0 )
     then // Zone is Unconditioned and space area is either not 
          // 100% Unconditioned or 100% IndirectlyConditioned    
       PostError("ThermalZone '%s' is Type = 'Unconditioned' and contains 
                  a mix of 'Unconditioned' and 'IndirectlyConditioned' space;
                  this is not permitted.", Name)
     else UNCHANGED
     endif endif endif endif
   else if( Type = "Plenum" .AND. PlenumArea != Area )
   then // Zone is Plenum and 100% of space area is not Plenum
     PostError("ThermalZone '%s' is Type = 'Plenum', but does not contain
                100% 'Plenum' space area; this is not permitted.", 
               Name)
   else if( Type = "Conditioned" .AND. DirectCondArea = 0 )
   then // Zone is Conditioned but has no DirectlyConditioned space
     PostError("ThermalZone '%s' is 'Conditioned', but does not contain any
                'DirectlyConditioned' space area; this is not permitted.", 
               Name)
   else UNCHANGED
   endif endif endif endif
  SIZING
    Type
  ANNUAL
    z:Type
ENDRULE
RULE NEW ThrmlZn:IsCond
  DATATYPE
    Integer
  DESCRIPTION
    "Flag that indicates if the zone type is Conditioned." 
  LONGFORM
    IsConditioned 
  HELP
    "Used to to calculate and report Proj:NumThrmlZnCond."    
  INPUTCLASS
    NotInput
  DEFAULT 
    if( Type = "Conditioned" )
    then 1
    else 0
    endif
  SIZING
    IsCond
  ANNUAL
    z:IsCond
ENDRULE
RULE ThrmlZn:CondgStatus
  DESCRIPTION
    "Flag that indicates the conditioning status of the zone for reporting."
  HELP
    "The flag values are:

     0 : ThrmlZn:Type = 'Plenum', or ThrmlZn:Type = 'Unconditioned' and comprised of
         100% Spc:CondgType = 'Unconditioned' space

     1 : ThrmlZn:Type = 'Conditioned'. In this case, the zone may be comprised of a mix of
         Spc:CondgType = 'DirectlyConditioned' or 'IndirectlyConditioned' space, or
         100% Spc:CondgType = 'DirectlyConditioned' space

     2 : ThrmlZn:Type = 'Unconditioned' but comprised of
         100% Spc:CondgType = 'IndirectlyConditioned' space" 
  INPUTCLASS
    NotInput
  DEFAULT 
    if( IsCond > 0 )
    then 1
    else if( Type = "Unconditioned" .AND. IndirectCondArea = FlrArea )
    then 2
    else 0
    endif endif
  ANNUAL
    if( IsCond > 0 )
    then 1
    else if( Type = "Unconditioned" .AND. IndirectCondArea = FlrArea )
    then 2
    else 0
    endif endif
ENDRULE



// ********** HVAC System Name References **************************************
// First, define the proposed terminal unit that serves the ThrmlZn (if applicable)
// Status of ThermalZone assignment to TerminalUnits
RULE ThrmlZn:TrmlUnitRefStatus
  DESCRIPTION
    "Enumeration that indicates status of the ThermalZone with respect its 
     assignment to the TrmlUnit:ZnServedRef property."
  HELP
    "Used to control list of available ThermalZones that can be assigned to 
     TrmlUnit:ZnServedRef property.

     Conditioned = The zone is not referenced via TrmlUnit:ZnServedRef, but it 
     is a valid, possible reference. The enumeration is titled 'Conditioned' to 
     facilitate the correct UI object creation phrase in combo box.  

     InValidAssigned = The TrmlUnitRefStatus is Invalid for one or more reasons, such as:

     - The zone is referenced via TrmlUnit:ZnServedRef by more than one TerminalUnit.

     - The zone is served by an AirSys, but is not referenced via TrmlUnit:ZnServedRef

     - The zone cannot be referenced by TrmlUnit:ZnServedRef. This is the case 
       if ThrmlZn:Type = Unconditioned or PlenumZone."
  INPUTCLASS
    NotInput
  OPTION
    Conditioned
    ConditionedAssigned
    Unconditioned
    UnconditionedAssigned
    InvalidAssigned
  DEFAULT
    if( IsCond > 0 )
    then
      if( IfValidAnd( NumTrmlUnitRefs > 0 ) )
      then "ConditionedAssigned"
      else "Conditioned"
      endif
    else 
    if( Type = "Unconditioned" )
    then 
      if( IfValidAnd( NumTrmlUnitRefs > 0 ) )
      then "UnconditionedAssigned"
      else "Unconditioned"
      endif
    else if( IfValidAnd( NumTrmlUnitRefs > 0 ) )
    then "InvalidAssigned"
    else UNDEFINED
    endif endif endif
  CHECKSIM
    if( ( LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType( "AirSys" ) .OR.
          LocalCompAssigned( VentSysRef ) = ComponentType( "AirSys" ) ) .AND.
        ( TrmlUnitRefStatus = "Conditioned" .OR. TrmlUnitRefStatus = "Unconditioned" ) .AND.
        HasUnknownHVAC < 1 )
    then
      if( IfValidAnd( VentSysRef:IsExhSys > 0 ) )
      then UNCHANGED // Exhaust systems don't need TrmlUnits
      else 
        PostError( "ThermalZone '%s' is served by an AirSys, but is not referenced
                   by a TerminalUnit. Revise model to include a TerminalUnit that
                   references this ThermalZone.", 
                   Name )
      endif
    else
    if( TrmlUnitRefStatus = "InvalidAssigned" .AND.
        ( IsCond > 0 .OR. Type = "Unconditioned" ) .AND.
        LocalCompAssigned( PriAirCondgSysRef[1] ) != ComponentType( "AirSys" ) .AND.
        LocalCompAssigned( VentSysRef ) != ComponentType( "AirSys" ) ) 
    then
      PostError( "ThermalZone '%s' is referenced by %g TerminalUnit(s), but the 
                 PrimaryAirConditioningSystem or VentilationSystem of the zone
                 is not an AirSystem. Revise model AirSystem and/or TerminalUnit
                 references to be consistent.", 
                 Name, NumTrmlUnitRefs )

    else
    if( TrmlUnitRefStatus = "InvalidAssigned" .AND. Type = "Plenum" ) 
    then
      PostError( "ThermalZone '%s' is referenced by %g TerminalUnit(s), but it is
                 Type = 'Plenum'. This is currently not supported.",
                 Name, NumTrmlUnitRefs )   
    else UNCHANGED
    endif endif endif
  SIZING
    UNDEFINED
ENDRULE
RULE ThrmlZn:TrmlUnitRef
  DESCRIPTION
    "The name of the TerminalUnit that serves the zone."
  INPUTCLASS
    NotInput
  DEFAULT
    MaxRevRefComp( TrmlUnit:ZnServedRef, TrmlUnit:Cnt )
  SIZING_PROPOSED
    if( Type = "Unconditioned" .OR. Type = "Plenum" .OR. 
        Proj:IsNoMech .OR. ( Proj:IsNoAddMech .AND. IfValidAnd( IsExistingHVAC <= 0 ) ) .OR.
        HasUnknownHVAC = 1 )
    then // Partial envelope for whole building or partial envelope for addition
         // and the system is not existing
      UNDEFINED
    else TrmlUnitRef
    endif 
  ANNUAL
    z:TrmlUnitRef
ENDRULE
RULE NEW ThrmlZn:PropZnSysCnt
  DATATYPE
    Integer
  LONGFORM
    ProposedZoneSystemCount
  DESCRIPTION
    "The number of proposed TerminalUnits or ZoneSystems that serve the zone."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
// TO DO: Revisit rule to confirm this works in all cases
    if( HasUnknownHVAC > 0 )
    then
      // This is partial compliance NewEnvelope, and HVAC systems are created,
      // one per TrmlUnit or ZnSys per ThermalZone
      HVACZnCntWithMult
    else if( LocalCompAssigned( TrmlUnitRef ) )
    then 
      // Number of proposed zone systems is the HVACZonesWithMultiplier
      // divided by the number of TerminalUnits serving the zone, including
      // system multipliers
      HVACZnCntWithMult
      ; Max( HVACZnCntWithMult / TrmlUnitRef:Cnt * TrmlUnitRef:SysCnt, 1 )
    else if( LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType( "ZnSys" ) )
      then
      // Number of proposed zone systems is the HVACZonesWithMultiplier
      // divided by the number of ZoneSystems serving the zone, including
      // system multipliers
      HVACZnCntWithMult
      ;Max( HVACZnCntWithMult / PriAirCondgSysRef[1]:Cnt, 1 )
    else 0
    endif endif endif
  SIZING_BASELINE
    zp:PropZnSysCnt
  ANNUAL
    z:PropZnSysCnt
ENDRULE


// Primary Air Conditioning (Heating/Cooling) System
RULE ThrmlZn:PriAirCondgSysRef[1]
  DESCRIPTION
    "The name of the air or zone system that is the principal source of heating
     and/or cooling for the ThermalZone."
  HELP
    "AirSystems or ZoneSystems are both valid object assignments for this 
     reference property. However, only one unique AirSystem can be assigned to 
     PriAirCondgSysRef and VentSysRef properties."
  INPUTCLASS 
    CondRequired
  DEFAULT
    if( LocalCompAssigned( TrmlUnitRef ) .AND. HasUnknownHVAC = 0 .AND. IsCond > 0 )
    then 
    // If applicable, default to the Parent AirSys of the TrmlUnit with the 
    // largest PriAirFlowMax that refernces the zone
      if( LocalCompAssigned( TrmlUnitRef:ParentAirSysRef ) )
      then TrmlUnitRef:ParentAirSysRef:Name
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKSIM
    if( IsCond > 0 .AND. 
        LocalCompAssigned( PriAirCondgSysRef[1] ) = 0 .AND.
        HasUnknownHVAC = 0 )
    then
      PostError("ThermalZone '%s' is Conditioned, but does not have a system
                 assigned to PrimaryAirConditioningSystem. Assign a system
                 or revise to Unconditioned/Plenum", Name )
    else             
    if( LocalCompAssigned( PriAirCondgSysRef[1] ) )
    then
      if( PriAirCondgSysRef[1]:IsExhSys )
      then 
        PostError("ThermalZone '%s' has PrimaryAirConditioningSystem[1] 
                   of Type = 'Exhaust'. This is not allowed.", Name )    
      else UNCHANGED
      endif 
    else UNCHANGED
    endif endif
  SIZING_PROPOSED
    if( Type = "Unconditioned" .OR. Type = "Plenum" .OR. 
        Proj:IsNoMech .OR. ( Proj:IsNoAddMech .AND. IfValidAnd( IsExistingHVAC <= 0 ) ) .OR.
        HasUnknownHVAC = 1 )
    then // Partial envelope for whole building or partial envelope for addition
         // and the system is not existing
      UNDEFINED
    else PriAirCondgSysRef[1]
    endif
//SIZING_BASELINE
// Sizing rules in BaselineHVACSystems.rule 
ENDRULE
// Ventilation System
RULE ThrmlZn:VentSysRef
  DESCRIPTION
    "The name of the air or zone system that provides ventilation air to the 
     ThermalZone. This is by default the same system as the primary air 
     conditioning system."
  HELP
    "AirSystems or ZoneSystems are both valid object assignments for this 
     reference property. By default, the ventilation system for the zone is defined
     to be the primary heating/cooling system. If the ventilation system is a supply air
     system that is different than the heating/cooling system, such as in the
     case of using a dedicated outdoor air system (DOAS), the VentSysRef must be 
     be an AirSystem, and the PriAirCondgSysRef must be a ZoneSystem. If the ventilation
     requirement is exhaust, such as a parking garage or an auto repair space, the
     exhaust system should be defined as the VentSysRef."
  INPUTCLASS 
    CondRequired
// See Spc-Ventilation.rule for rule ordering purposes
  CHECKCODE : T24N_2016
    if( IsCond > 0 .AND.
        VentSrc = "Forced" )
    then 
      if( LocalCompAssigned( VentSysRef ) = 0 .AND. 
          HasUnknownHVAC = 0 )
      then // VentSysRef is NOT defined 
        PostError("ThermalZone '%s' has 'Forced' ventilation, but a
                   VentilationSystem is not defined.", Name)
      else
      if( LocalCompAssigned( VentSysRef ) .AND. 
          HasUnknownHVAC = 0 )
      then // VentSysRef is defined 
        if( VentSysRef:IsValidVentSys = 0 )
        then
          PostError("The VentilationSystem for ThermalZone '%s' is a
                     '%s' system. This system cannot be defined as a
                     ventilation system.", Name, VentSysRef:Type) 
        else UNCHANGED
        endif
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif
  CHECKCODE : T24N
    if( IsCond > 0 .AND. VentSrc = "Forced" .AND. VentFlow > 0 )
    then 
      if( LocalCompAssigned( VentSysRef ) = 0 .AND. HasUnknownHVAC = 0 )
      then // VentSysRef is NOT defined
        PostError("ThermalZone '%s' has 'Forced' ventilation, but the
                   VentilationSystem is not defined.", Name)
      else
      if( LocalCompAssigned( VentSysRef ) .AND. HasUnknownHVAC = 0 )
      then // VentSysRef is defined
        if( VentSysRef:IsValidVentSys = 0 )
        then
          PostError("The VentilationSystem for ThermalZone '%s' is a
                     '%s'. This system cannot be defined as a
                     ventilation system.", Name, VentSysRef:Type) 
        else
        if( IsHighRiseRes > 0 .AND. IfValidAnd( PriAirCondgSysRef[1] = VentSysRef ) )
        then 
          PostError("ThermalZone '%s' is dwelling unit zone, with the same system
                     defined for both heat/cool and ventilation, which would require
                     the HVAC system to run continuously. This is not allowed;
                     ventilation must be provided by a separate system.",
                     Name, VentSysRef:Type)
        else
        if( ( VentSysRef:Type = "DOASCV" .OR. VentSysRef:Type = "DOASVAV" ) .AND. 
            IfValidAnd( PriAirCondgSysRef[1] = VentSysRef ) ) 
        then // See ticket 3524
          PostError("ThermalZone '%s' is a conditioned zone that is ventilated and 
                     conditioned by a Type = DOAS system. This is currently not supported.
                     Change the zone to 'Unconditioned' or specify at minimum a separate
                     heating system.",
                     Name, VentSysRef:Type)             
        else UNCHANGED
        endif endif endif
      else UNCHANGED
      endif endif
    else
    if( LocalCompAssigned( VentSysRef ) .AND. 
        PropVentSysIsExh = 0 .AND. 
        PropExhSysIsBalanced = 0 .AND.
        VentFlow = 0 )
    then 
      PostError("ThermalZone '%s' has VentilationSystemReference defined, but the ventilation flow
                 is zero. Remove reference to this system. If the space requires exhaust, specify
                 an exhaust system using ExhaustSystemReference", Name)
    else UNCHANGED
    endif endif
// #3220 Allow two air system to be modeled in CBECC - JP
;  CHECKSIM
;    if( LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType( "AirSys" ) .AND.
;        LocalCompAssigned( VentSysRef ) = ComponentType( "AirSys" ) ) 
;    then
;      if( IfValidAnd( VentSysRef:IsExhSys = 0 ) .AND.
;          VentSysRef:Name != PriAirCondgSysRef[1]:Name )
;      then
;         PostError("The VentilationSystem and PrimaryAirConditioningSystem[1] of
;                    ThermalZone '%s' are two different AirSystems. This is currently
;                    not supported. Only one of the two systems assigned to a zone
;                    can be an AirSystem.", Name)
;      else UNCHANGED
;      endif
;    else UNCHANGED
;    endif
  SIZING_PROPOSED
    if( Type = "Plenum" .OR. 
        Proj:IsNoMech .OR. ( Proj:IsNoAddMech .AND. IfValidAnd( IsExistingHVAC <= 0 ) ) .OR.
        HasUnknownHVAC = 1 )
    then // Partial envelope for whole building or partial envelope for addition
         // and the system is not existing
      UNDEFINED
    else if( VentSrc = "Forced" )
    then VentSysRef
    else UNDEFINED
    endif endif
; SIZING_BASELINE
// Sizing rules in BaselineHVACSystems.rule
ENDRULE
// Secondary Air Conditioning System (zonal system only)
RULE ThrmlZn:PriAirCondgSysRef[2] 
  DESCRIPTION
    "The name of the air or zone system that is the secondary source of heating
     and/or cooling for the ThermalZone."
  HELP
    "ZoneSystems are the only valid object assignments for this reference property."
  INPUTCLASS 
    Optional
  CHECKSIM
    if( LocalCompAssigned( PriAirCondgSysRef[2] ) .AND.
        LocalCompAssigned( PriAirCondgSysRef[1] ) = 0 )
    then
      PostError("ThermalZone '%s' has a system defined for
                 PrimaryAirConditioningSystem[2], but not for [1].
                 the first reference should be used before defining
                 a second system.", Name )       
    else      
    if( LocalCompAssigned( PriAirCondgSysRef[2] ) = ComponentType( "AirSys" ) )
    then
      PostError("ThermalZone '%s' has an AirSystem defined for
                 PrimaryAirConditioningSystem[2], which is not supported.
                 The second system reference must be ZoneSystem.", Name )       
    else
    if( LocalCompAssigned( PriAirCondgSysRef[2] ) )
    then
      if( PriAirCondgSysRef[2]:IsExhSys )
      then 
        PostError("ThermalZone '%s' has PrimaryAirConditioningSystem[2] 
                   of Type = 'Exhaust'. This is not allowed.", Name )     
      else UNCHANGED
      endif 
    else UNCHANGED
    endif endif endif
  SIZING_PROPOSED
    if( Type = "Unconditioned" .OR. Type = "Plenum" .OR. 
        Proj:IsNoMech .OR. ( Proj:IsNoAddMech .AND. IfValidAnd( IsExistingHVAC <= 0 ) ) .OR.
        HasUnknownHVAC = 1 )
    then // Partial envelope for whole building or partial envelope for addition
         // and the system is not existing
      UNDEFINED
    else PriAirCondgSysRef[2]
    endif  
//SIZING_BASELINE
// Sizing rules in BaselineHVACSystems.rule   
ENDRULE

// Set reference to VentSysRef at Space. 
RULE Spc:VentSysRef
  DESCRIPTION
    "The ThermalZone ventilation system, defined at Spc for data access purposes."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( LocalCompAssigned( ThrmlZnRef ) )
    then
      if( LocalCompAssigned( ThrmlZnRef:VentSysRef ) )
      then ThrmlZnRef:VentSysRef
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** Plenum Zone References *******************************************
// Moved to HVACSecondary-AirSegment.rule for ordering purposes


// ------------ HVAC zone count and thermal zone multiplier --------------------
RULE NEW ThrmlZn:NumSpcs
  DATATYPE
    Integer
  LONGFORM
    NumberSpaces
  DESCRIPTION
    "The number of spaces that comprise the ThermalZone."  
  HELP
   "ThermalZones shall contain no more than ten different space functions, 
    which shall only be combined if the spaces have similar space conditioning 
    requirements."
  REFERENCE 
    NACM Section 2.3.3
  INPUTCLASS
    NotInput
  DEFAULT
    CountRefs( Spc:ThrmlZnRef ) 
  CHECKSIM
    if( NumSpcs < 1 )
    then
      PostError("ThermalZone '%s' has no Spaces that reference it.", Name)
    else UNCHANGED
    endif
ENDRULE
RULE ThrmlZn:HVACZnCnt
  DESCRIPTION
    "The number of building HVACZones represented by the modeled ThermalZone."  
  HELP
    "An HVAC zone is a physical space within the building that has its own thermostat
     and system for maintaining thermal comfort. HVAC zones are identified
     on the HVAC plans. HVAC zones should not be split between ThermalZones, 
     but a ThermalZone may include more than one HVAC zone. 

     This property should be > 1 only if individual HVAC zones have been combined 
     into a single ThermalZone for modeling purposes. An example is a block of
     multifamily dwelling units with similar orientation and the same HVAC 
     systems serving each unit combined into a single ThermalZone. In this
     case, the HVACZoneCount and the AirSystem's TerminalUnit:Count or 
     ZoneSystem:Count property should be consistent with eachother, and the
     capacities entered for systems should be for one unit. 

     The following SDD modeling rules for HVACZoneCount and combining into a 
     single ThermalZone must be followed:
 
     a) HVACZones are adjacent to eachother and have similar exterior orientation.

     b) Similar to combining Spaces into a ThermalZone, the combined HVACZones 
       do not span multiple Stories."
  REFERENCE 
    NACM Section 2.2.1
  INPUTCLASS
    Default
  REPORTPRECISION
    0
  MINIMUM
    0
  COMMONMAXIMUM
    10
  DEFAULT 
    if( IsCond > 0 )
    then
      if( Proj:AutoHardSize > 0 )
      then
        if( LocalCompAssigned( PriAirCondgSysRef[1] ) > 0 )
        then // Has Htg/ClgSys
          if( IfValidAnd( PriAirCondgSysRef[1]:IsSglZnSys > 0 ) )
          then // Is a single-zone system, set to Cnt of that system
            ValidOr( PriAirCondgSysRef[1]:Cnt, 1 ) / ValidOr( Mult, 1 )
          else 1 
          endif
        else 
        if( LocalCompAssigned( VentSysRef ) > 0 .AND.
            LocalCompAssigned( TrmlUnitRef ) > 0 )
        then // Is a DOAS AirSys or multizone system, set to Cnt of TrmlUnit
          ValidOr( TrmlUnitRef:Cnt, 1 ) / ValidOr( Mult, 1 )
        else 1
        endif endif
      else 1
      endif   
    else 0
    endif
  CHECKSIM
    if( Type = "Conditioned" .AND. HVACZnCnt < 1 )
    then 
      PostError("ThermalZone '%s' has an HVACZoneCount of < 1 even though it is
                 a conditioned zone.", Name)
    else UNCHANGED
    endif
  SIZING
    HVACZnCnt
ENDRULE
RULE ThrmlZn:Mult
  DESCRIPTION
    "A multiplier on ThermalZone inputs."  
  HELP
    "This property is used to simuluate multiples of a single ThermalZone. The
     following modeling rules for Multipliers must be followed: 

     a) All spaces that are combined into a ThermalZone must have the same Multiplier

     b) Spaces that are combined into a ThermalZone cannot span multiple Stories"  
  REFERENCE 
    NACM Section 2.3.1
    NACM Section 5.3
  INPUTCLASS
    NotInput
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd( MaxRevRef( Spc:ThrmlZnRef, Spc:Mult ) > 0 ) ) 
    then MaxRevRef( Spc:ThrmlZnRef, Spc:Mult )
    else 1
    endif
  CHECKSIM
    if( NumSpcs > 1 .AND.
        Mult != ValidOr( MinRevRef( Spc:ThrmlZnRef, Spc:Mult ), 1 ) )
    then
      PostError("The spaces combined into ThermalZone '%s' must have the same
                 multiplier.", Name)
    else UNCHANGED
    endif
  SIZING
    MaxRevRef( Spc:ThrmlZnRef, Spc:Mult )
  ANNUAL
    z:Mult
ENDRULE

RULE NEW ThrmlZn:HVACZnCntWithMult
  DATATYPE
    Integer
  LONGFORM
    HVACZoneCountWithMult
  DESCRIPTION
    "The number of HVAC zones represented in the model."
  INPUTCLASS
    NotInput
  DEFAULT
    HVACZnCnt * Mult
  SIZING
    HVACZnCntWithMult
  ANNUAL
    z:HVACZnCntWithMult   
ENDRULE

RULE NEW ThrmlZn:CondHVACZnCntWithMult
  DATATYPE
    Integer
  LONGFORM
    ConditionedHVACZoneCountWithMult
  DESCRIPTION
    "The number of conditioned HVAC zones with multiplier."
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsCond > 0 )
    then HVACZnCnt * Mult
    else UNDEFINED
    endif
  SIZING
    CondHVACZnCntWithMult
  ANNUAL
    z:CondHVACZnCntWithMult   
ENDRULE

// **********  Floor Area ******************************************************
RULE ThrmlZn:FlrArea
  DESCRIPTION
    "The gross floor area of a ThermalZone."  
  HELP
    "The gross floor area of a ThermalZone (before multiplier is applied); 
     including walls and minor spaces for mechanical or electrical services 
     such as chases that are not assigned to other ThermalZones.  
     Larger mechanical spaces and electrical rooms should not be combined."  
  REFERENCE 
    NACM Section 5.3.1
  INPUTCLASS
    NotInput
  UNITS 
    ft2
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "Plenum" ) 
    then 0
    else SumRevRef( Spc:ThrmlZnRef, Spc:FlrArea )
    endif
  SIZING
    FlrArea
  ANNUAL
    z:FlrArea
ENDRULE

RULE NEW ThrmlZn:FlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    FloorAreaWithMultiplier
  DESCRIPTION
    "The gross floor area of a ThermalZone, with zone multiplier."  
  INPUTCLASS
    NotInput
  UNITS 
    ft2
  DEFAULT
    FlrArea * Mult
  SIZING
    FlrAreaWithMult
  ANNUAL
    z:FlrAreaWithMult
ENDRULE

// Conditioned floor area
RULE NEW ThrmlZn:CondFlrArea
  DATATYPE
    Float
  LONGFORM
    ConditionedFloorArea
  DESCRIPTION
    "The gross conditioned floor area of a ThermalZone."  
  REFERENCE 
    NACM Section 5.3.1
  INPUTCLASS
    NotInput
  UNITS 
    ft2
  DEFAULT
    if( IsCond > 0 )
    then FlrArea
    else 0
    endif  
  CHECKCODE
    if( ( CompRmArea > 0  .AND. ProcArea != CompRmArea ) .OR.
        ( LabArea > 0     .AND. ProcArea != LabArea  ) .OR.
        ( CommKitArea > 0 .AND. ProcArea != CommKitArea  ) )
    then
      PostError("ThermalZone '%s' is comprised of different types of process spaces. This is not allowed.", Name)
    else if( ProcArea > 0 .AND. ProcArea != CondFlrAreaWithMult )
    then
      PostError("ThermalZone '%s' is comprised of both process
                 and non-process space. This is not allowed.", Name)
    else UNCHANGED
    endif endif
  SIZING
    CondFlrArea
  ANNUAL
    z:CondFlrArea
ENDRULE

RULE NEW ThrmlZn:CondFlrAreaWithMult
  DATATYPE
    Float
  LONGFORM
    ConditionedFloorAreaWithMultiplier
  DESCRIPTION
    "The gross conditioned floor area of a ThermalZone, with zone multiplier."  
  INPUTCLASS
    NotInput
  UNITS 
    ft2
  DEFAULT
    CondFlrArea * Mult
  SIZING
    CondFlrAreaWithMult
  ANNUAL
    z:CondFlrAreaWithMult
ENDRULE

// Volume
RULE NEW ThrmlZn:Vol
  DATATYPE
    Float
  LONGFORM
    Volume
  DESCRIPTION
    "The gross volume of a ThermalZone."  
  INPUTCLASS
    NotInput
  UNITS 
    ft3
  DEFAULT
    SumRevRef( Spc:ThrmlZnRef, Spc:Vol )
  SIZING
    SumRevRef( Spc:ThrmlZnRef, Spc:Vol )
  ANNUAL
    z:Vol
ENDRULE

RULE NEW ThrmlZn:VolWithMult
  DATATYPE
    Float
  LONGFORM
    VolumeWithMuliplier
  DESCRIPTION
    "The gross volume of a ThermalZone, with zone multiplier."   
  INPUTCLASS
    NotInput
  UNITS 
    ft3
  DEFAULT
    Vol * Mult
  SIZING
    Vol * Mult
  ANNUAL
    z:VolWithMult
ENDRULE

// Volume - to be passed into simulation engine
RULE ThrmlZn:VolSim
  DESCRIPTION
    "The gross volume of a ThermalZone (to be passed into simulation)."  
  INPUTCLASS
    NotInput
  UNITS 
    ft3
  REPORTPRECISION
    0
  DEFAULT
    if (Proj:IsDetailedGeometry)
    then UNDEFINED
    else Vol
    endif
  SIZING
    if (Proj:IsDetailedGeometry)
    then UNDEFINED
    else z:Vol
    endif
  ANNUAL
    z:VolSim
ENDRULE


// Establish occupancy class of thermal zones based on Spc:OccClass
// Conditioned floor area
// ------------ Calculate ThrmlZn floor area -----------------------------------
// Residential
RULE NEW ThrmlZn:ResCondFlrArea
  DATATYPE
    Float
  LONGFORM
    ResidentialFloorArea
  DESCRIPTION
    "The total ThrmlZn residential (conditioned) floor area." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2  
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:ResCondFlrArea )
  SIZING
    ResCondFlrArea
  ANNUAL
    z:ResCondFlrArea
ENDRULE

// Nonresidential
RULE NEW ThrmlZn:NonResCondFlrArea
  DATATYPE
    Float
  LONGFORM
    NonresidentialFloorArea
  DESCRIPTION
    "The total ThermalZone nonresidential (conditioned) floor area." 
  INPUTCLASS
    NotInput
  MINIMUM 
    0
  UNITS 
    ft2    
  DEFAULT 
    SumRevRef( Spc:ThrmlZnRef, Spc:NonResCondFlrArea )
  SIZING
    NonResCondFlrArea
  ANNUAL
    z:NonResCondFlrArea
ENDRULE

RULE ThrmlZn:OccClass
  DESCRIPTION
    "The occupancy classification of the ThermalZone" 
  INPUTCLASS
    NotInput
  OPTION
    Nonresidential
    Residential
  DEFAULT
    if( ResCondFlrArea > 0 )
    then "Residential"
    else "Nonresidential"
    endif  
  CHECKCODE
    if( NonResCondFlrArea > 0 .AND.
        ResCondFlrArea > 0 )
    then PostError( "ThermalZone '%s' includes spaces of residential and nonresidential 
                    occupancy classifications; this is not permitted.", Name )
    else UNCHANGED
    endif 
  SIZING
    OccClass
  ANNUAL
    z:OccClass
ENDRULE

RULE NEW ThrmlZn:IsRes
  DATATYPE
    Integer
  LONGFORM
    IsResidential
  DESCRIPTION
    "An integer that indicates if the space is residential." 
  INPUTCLASS
    NotInput
  DEFAULT
    if( OccClass = "Residential" )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ThrmlZn:IsHighRiseRes
  DATATYPE
    Integer
  LONGFORM
    IsHighRiseResidential
  DESCRIPTION
    "An integer that indicates if the zone is high-rise residential." 
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsRes > 0 .AND. 
        SumRevRef( Spc:ThrmlZnRef, Spc:HighRiseResCondFlrArea ) = ResCondFlrArea )
    then 1
    else 0
    endif
  CHECKCODE
    if( IsRes > 0 .AND. 
        SumRevRef( Spc:ThrmlZnRef, Spc:HighRiseResCondFlrArea ) > 0 .AND.
        SumRevRef( Spc:ThrmlZnRef, Spc:HotelMotelGuestRmCondFlrArea ) > 0 )
    then
      PostError("ThermalZone '%s' combines high-rise and hotel/motel residential
                 spaces into one zone. This is not allowed.", Name)
    else UNCHANGED
    endif
ENDRULE

RULE NEW ThrmlZn:IsHotelMotelGuestRm
  DATATYPE
    Integer
  LONGFORM
    IsHotelMotelGuestRoom
  DESCRIPTION
    "An integer that indicates if the zone is hotel/motel guest room." 
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsRes > 0 .AND. 
        SumRevRef( Spc:ThrmlZnRef, Spc:HotelMotelGuestRmCondFlrArea ) = ResCondFlrArea )
    then 1
    else 0
    endif
ENDRULE

// ********** Zone 'Enclosed' Status ******************************************
RULE NEW ThrmlZn:NotEnclosedZn
  RULESETS
    S901G ECBC
  DATATYPE
    Integer
  LONGFORM
    NotEnclosedZone
  DESCRIPTION
    "A flag that indicates whether the zone is 'NotEnclosed' for determining
     baseline envelope and system requirements." 
  INPUTCLASS
    NotInput
  DEFAULT
    MaxRevRef( Spc:ThrmlZnRef, Spc:NotEnclosed )
  CHECKCODE
    if( MinRevRef( Spc:ThrmlZnRef, Spc:NotEnclosed ) != NotEnclosedZn )
    then 
      PostError("ThermalZone '%s' is comprised of enclosed/unenclosed space
                 functions. This is not allowed.", Name)
    else UNCHANGED
    endif
  SIZING
    NotEnclosedZn
ENDRULE

RULE NEW TrmlUnit:NotEnclosedZn
  RULESETS
    S901G ECBC
  DATATYPE
    Integer
  LONGFORM
    NotEnclosedZone
  DESCRIPTION
    "A flag that indicates whether the zone served by the Terminal is 'NotEnclosed'" 
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalCompAssigned( ZnServedRef ) .AND. ServesThrmlZn )
    then ZnServedRef:NotEnclosedZn
    else 0
    endif
  SIZING
    if( LocalCompAssigned( ZnServedRef ) .AND. ServesThrmlZn )
    then ZnServedRef:NotEnclosedZn
    else 0
    endif
ENDRULE


// ---------- Identify which ThermalZones are control/slave zones -------------
// A flag that indicates if the TrmlUnit is Type = Uncontrolled
RULE NEW TrmlUnit:IsUncontrolled
  DATATYPE
    Integer
  LONGFORM
    IsUncontrolled
  INPUTCLASS
    NotInput
  SIZING
    if( Type = "Uncontrolled" )
    then 1
    else 0
    endif   
ENDRULE

// Identify if the ThrmlZn is the CtrlZn for the PriAirCondgSys that serves it.
RULE NEW ThrmlZn:IsCtrlZn
  DATATYPE
    Integer
  LONGFORM
    IsControlZone
  DESCRIPTION
    "Boolean that specifies if a ThermalZone is a control zone"  
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( IsCond > 0 .AND. 
        ( Proj:IsNoMech .OR. 
          ( Proj:IsNoAddMech .AND. IsExistingHVAC <= 0 ) .OR.
          IfValidAnd( HasUnknownHVAC = 1 ) ) )
    then // Partial envelope for whole building or partial envelope for addition
         // and the system is not existing, all conditioned zones are assumed control zones
      1 
    else
    if( IsCond > 0 .AND. 
       ( LocalCompAssigned( PriAirCondgSysRef[1] ) .OR. 
         LocalCompAssigned( VentSysRef ) .OR. 
         LocalCompAssigned( PriAirCondgSysRef[2] ) ) )
    then // The zone is conditioned and is served by a proposed HVAC system
      if( IsRes )
      then // All baseline residential ThermalZones are assumed to be a CtrlZn,
           // even if proposed design has slave zones
        1
      else if( SumRevRef( TrmlUnit:ZnServedRef, TrmlUnit:IsUncontrolled ) = 0 )
      then // If true, the terminal unit is not Type = Uncontrolled, rather it is a 
           // VAV or other terminal unit controlled by a zone thermostat
        1
      else if( LocalCompAssigned( PriAirCondgSysRef[1] ) )
      then
        if( PriAirCondgSysRef[1]:CtrlZnRef = Name .OR. 
            LocalCompAssigned( PriAirCondgSysRef[1] ) = ComponentType ( "ZnSys" ) )
        then // The current zone is control zone for the PrimAirCondgSys, 
             // or, the PrimAirCondgSys is a ZnSys
           1
        else // Zone is a slave zone of a nonresidential system
          -1
        endif
      else 0
      endif endif endif
    else 0
    endif endif
  SIZING_BASELINE
    zp:IsCtrlZn 
  ANNUAL
    z:IsCtrlZn      
ENDRULE
// Transfer the value to Space
RULE NEW Spc:IsCtrlZn
  DATATYPE
    Integer
  LONGFORM
    IsControlZone
  DESCRIPTION
    "Boolean that specifies if the space's ThermalZone is a control zone"  
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( LocalCompAssigned( ThrmlZnRef ) )
    then ThrmlZnRef:IsCtrlZn
    else 0
    endif
  SIZING_BASELINE
    zp:IsCtrlZn 
  ANNUAL
    z:IsCtrlZn      
ENDRULE

// Specify the name of the ThrmlZn that is the CtrlZn for all zones served by a 
// proposed system.  This ensures that slave zones of single-zone systems are 
// maintained in the proposed and baseline designs.
RULE NEW ThrmlZn:SlaveCtrlZnRef
  DATATYPE
    ThrmlZn
  LONGFORM
    SlaveControlZoneReference
  DESCRIPTION
    "For single-zone systems with slave zones, this property indicates the name 
     of the ThermalZone that is the control zone for the proposed single-zone 
     system."
  HELP
    "If the proposed design is single-zone, and the baseline design is as well,
     slave zone status is preserved in the baseline design."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( IsCtrlZn = -1 )
    then PriAirCondgSysRef[1]:CtrlZnRef
    else UNDEFINED
    endif
  SIZING_BASELINE
    zp:SlaveCtrlZnRef
  ANNUAL
    z:SlaveCtrlZnRef 
ENDRULE

// Set flags to indicate whether a system HasClg or HasHtg
RULE NEW AirSys:HasClg
  DATATYPE
    Integer
  LONGFORM
    HasCooling
  DESCRIPTION
    "A flag that indicates if the system has cooling capacity."
  INPUTCLASS 
    NotInput
; DEFAULT : T24N_2016
;   if( IfValidAnd( ClgCap > 1 ) .OR. 
;       IfValidAnd( HasIndirectEvapClr > 0 ) .OR.
;       IfValidAnd( HasDirectEvapClr > 0 ) )
;   then 1
;   else 0
;   endif
// Evap cooling not considered mechanical cooling for 2019, per LF on CEC call
  DEFAULT
    if( IfValidAnd( ClgCap > 1 ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ZnSys:HasClg
  DATATYPE
    Integer
  LONGFORM
    HasCooling
  DESCRIPTION
    "A flag that indicates if the system has cooling capacity."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( ClgCap > 1 ) )
    then 1 // ZnSys can't have evap cooling
    else 0
    endif
ENDRULE

RULE NEW AirSys:HasHtg
  DATATYPE
    Integer
  LONGFORM
    HasHeating
  DESCRIPTION
    "A flag that indicates if the system has heating capacity."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( HtgCap > 1 ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ZnSys:HasHtg
  DATATYPE
    Integer
  LONGFORM
    HasHeating
  DESCRIPTION
    "A flag that indicates if the system has heating capacity."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( HtgCap > 1 ) )
    then 1
    else 0
    endif
ENDRULE

// Specify zone/spaces that are served by systems with cooling capacity
RULE NEW ThrmlZn:HasClg
  DATATYPE
    Integer
  LONGFORM
    HasCooling
  DESCRIPTION
    "A flag that indicates if a ThermalZone is served by a system with cooling
     capacity."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IsCond > 0 .AND. HasUnknownHVAC = 1 ) 
    then 1
    else 
    if( LocalCompAssigned( PriAirCondgSysRef[1] ) )
    then 
      if( LocalCompAssigned( PriAirCondgSysRef[2] ) )
      then // Second system assignment can only be a ZnSys
// Exclude VentSys from the criteria
;        if( LocalCompAssigned( VentSysRef ) )
;        then 
;          if( IfValidAnd( VentSysRef:ClgCap > 0 ) .OR. 
;              IfValidAnd( VentSysRef:HasIndirectEvapClr > 0 ) .OR.
;              IfValidAnd( VentSysRef:HasDirectEvapClr > 0 ) )
;          then 1
;          else 0
;          endif
;        else
        if( IfValidAnd( PriAirCondgSysRef[1]:HasClg > 0 ) .OR. 
            IfValidAnd( PriAirCondgSysRef[2]:HasClg > 0 ) )
        then 1
        else 0
        endif ;endif
      else
      if( IfValidAnd( PriAirCondgSysRef[1]:HasClg > 0 ) )
      then 1
      else 0
      endif endif
    else 0
    endif endif
  SIZING_PROPOSED  
    HasClg
ENDRULE

RULE NEW Spc:HasClg
  DATATYPE
    Integer
  LONGFORM
    HasCooling
  DESCRIPTION
    "A flag that indicates if a space is served by a system with cooling
     capacity."
  INPUTCLASS 
    NotInput 
  DEFAULT  
    if( ThrmlZnRef:HasClg > 0 )
    then 1
    else 0
    endif          
  SIZING_PROPOSED
    HasClg
ENDRULE

// Specify zone/spaces that are served by systems with heating capacity
RULE NEW ThrmlZn:HasHtg
  DATATYPE
    Integer
  LONGFORM
    HasHeating
  DESCRIPTION
    "A flag that indicates if a ThermalZone is served by a system with cooling
     capacity."
  INPUTCLASS 
    NotInput      
  DEFAULT 
    if( IsCond > 0 .AND. HasUnknownHVAC = 1 ) 
    then 1
    else 
    if( LocalCompAssigned( PriAirCondgSysRef[1] ) )
    then 
      if( LocalCompAssigned( PriAirCondgSysRef[2] ) )
      then // Second system assignment can only be a ZnSys
// Exclude VentSys from the criteria
;        if( LocalCompAssigned( VentSysRef ) )
;        then 
;          if( IfValidAnd( VentSysRef:HtgCap > 0 ) )
;          then 1
;          else 0
;          endif
;        else
        if( IfValidAnd( PriAirCondgSysRef[2]:HasHtg > 0 ) .OR.
            IfValidAnd( PriAirCondgSysRef[1]:HasHtg > 0 ) )
        then 1 // PriSys[1] or PriSys[2] have htg.
        else 0
        endif ;endif
      else
      if( IfValidAnd( PriAirCondgSysRef[1]:HasHtg > 0 ) )
      then 1
      else 0
      endif endif
    else 0
    endif endif 
  SIZING_PROPOSED
    HasHtg
ENDRULE

RULE NEW Spc:HasHtg
  DATATYPE
    Integer
  LONGFORM
    HasHeating
  DESCRIPTION
    "A flag that indicates if a space is served by a system with cooling
     capacity."
  INPUTCLASS 
    NotInput
  DEFAULT  
    if( IfValidAnd( ThrmlZnRef:HasHtg > 0 ) )
    then 1
    else 0
    endif             
  SIZING_PROPOSED
    HasHtg
ENDRULE

RULE NEW ThrmlZn:HasHtRcvryDOAS
  DATATYPE
    Integer
  LONGFORM
    HasHeatRecoveryDOAS
  DESCRIPTION
    "A flag that indicates if a thermal zone is served by a DOAS with heat recovery."
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalCompAssigned( VentSysRef ) == ComponentType( "AirSys" ) )
    then
      if( ( VentSysRef:Type = "DOASCV" .OR. VentSysRef:Type = "DOASVAV" ) .AND. IfValidAnd( VentSysRef:HasHtRcvry > 0 ) )
      then 1
      else 0
      endif
    else if( LocalCompAssigned( VentSysRef ) == ComponentType( "ZnSys" ) )
    then
      if( IfValidAnd( VentSysRef:HasHtRecvry > 0 ) )
      then 1
      else 0
      endif    
    else 0
    endif endif
ENDRULE
RULE NEW AirSys:PairedWithHtRcvryDOAS
  DATATYPE
    Integer
  LONGFORM
    PairedWithHeatRecoveryDOAS
  DESCRIPTION
    "A flag that indicates if a air system is paired with a DOAS with heat recovery."
  INPUTCLASS
    NotInput
  DEFAULT
    if( CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) + CountRefs( ThrmlZn:PriAirCondgSysRef[2] ) > 0 .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasHtRcvryDOAS ) + SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:HasHtRcvryDOAS ) > 0 .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[1], ThrmlZn:HasHtRcvryDOAS ) = 
          CountRefs( ThrmlZn:PriAirCondgSysRef[1] ) .AND.
        SumRevRef( ThrmlZn:PriAirCondgSysRef[2], ThrmlZn:HasHtRcvryDOAS ) = 
          CountRefs( ThrmlZn:PriAirCondgSysRef[2] ) )
    then 1
    else 0
    endif 
  SIZING
    ValidOr( PairedWithHtRcvryDOAS, 0 ) // for PropVentOnlyAirSys and BaseSys
ENDRULE
// QC rules for ThermalZone

// Number of building stories 
RULE NEW ThrmlZn:NumStory
  DATATYPE
    Integer
  LONGFORM
    NumberBuildingStory
  DESCRIPTION
    "The number of unique Story parents for spaces that reference a ThrmlZn."
  HELP
    "Spaces combined into a ThermalZone must all be children of the same Story."
  INPUTCLASS 
    NotInput
  DEFAULT
    CountUniqueParentRefs( Spc:ThrmlZnRef )
  CHECKCODE
    if( NumStory > 1)
    then 
      PostError( "ThermalZone '%s' is comprised of spaces that span different 
                 BuildingStory objects. This is currently not allowed.", Name )
    else UNCHANGED
    endif   
  SIZING
    NumStory
  ANNUAL
    z:NumStory            
ENDRULE
// The building story of the ThrmlZn
RULE NEW ThrmlZn:SpcWithMaxAreaRef
  DATATYPE
    Spc
  LONGFORM
    SpaceWithMaximumArea
  DESCRIPTION
    "The name of the Space with the largest area references the ThrmlZn."
  INPUTCLASS 
    NotInput
  DEFAULT
    MaxRevRefComp( Spc:ThrmlZnRef, Spc:Area )
  SIZING
    MaxRevRefComp( Spc:ThrmlZnRef, Spc:Area )
ENDRULE

// The building story of the ThrmlZn
RULE NEW ThrmlZn:StoryRef
  DATATYPE
    Story
  LONGFORM
    BuildingStoryReference
  DESCRIPTION
    "The name of the parent Story of the largest Space (by FlrArea) that references
     the ThermalZone."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( LocalCompAssigned( SpcWithMaxAreaRef ) )
    then SpcWithMaxAreaRef:ParentStoryRef
    else UNDEFINED
    endif      
ENDRULE


//Number of TerminalUnits that reference a ThermalZone
RULE NEW ThrmlZn:NumTrmlUnitRefs
  DATATYPE
    Integer
  LONGFORM
    NumberTerminalUnitReferences
  DESCRIPTION
    "The number of references to the ThermalZone via TrmlUnit:ZnServedRef property."
  INPUTCLASS
    NotInput 
  HELP
    "For TerminalUnits, this number should not be > 1."
  DEFAULT
    CountRefs(TrmlUnit:ZnServedRef)   
// #3220 Allow two air system to be modeled in CBECC - JP    
;  CHECKSIM 
;    if( ( TrmlUnitRefStatus = "ConditionedAssigned" .OR. 
;          TrmlUnitRefStatus = "UnconditionedAssigned" )
;        .AND.
;        NumTrmlUnitRefs > 1 ) 
;    then
;      PostError( "ThermalZone '%s' is referenced by %g TerminalUnits. The TerminalUnit 
;                 with the largest primary airflow serving the zone is '%s'. Revise
;                 the model so each ThermalZone is only served by one TerminalUnit.", 
;                 Name, NumTrmlUnitRefs, TrmlUnitRef )
;    else UNCHANGED
;    endif
  SIZING
    UNDEFINED
  ANNUAL
    CountRefs( TrmlUnit:ZnServedRef )
ENDRULE    
// Status of ThermalZone assignments to ZoneSystems
RULE NEW ZnSys:NumAirCondgSysRefs
  DATATYPE
    Integer
  LONGFORM
    NumberAirConditioningSystemReferences
  DESCRIPTION
    "The number of references to the ZnSys via ThrmlZn:PriAirCondgSysRef property."
  HELP
    "For ZoneSystems, this number should not be > 1."
  INPUTCLASS
    NotInput 
  DEFAULT
    CountRefs( ThrmlZn:PriAirCondgSysRef )
  CHECKSIM
    if( NumAirCondgSysRefs > 1 ) 
    then
      PostError( "ZoneSystem '%s' is referenced by %g ThermalZones. Revise
                 the model so each ZoneSystem is assigned to no more than one
                 ThermalZone.", Name, NumAirCondgSysRefs )
    else UNCHANGED
    endif
  SIZING
    UNDEFINED
  ANNUAL
    CountRefs( ThrmlZn:PriAirCondgSysRef )
ENDRULE
RULE NEW ZnSys:NumVentSysRefs
  DATATYPE
    Integer
  LONGFORM
    NumberVentilationSystemReferences
  DESCRIPTION
    "The number of references to the AirSys via ThrmlZn:VentSysRef property." 
  INPUTCLASS
    NotInput 
  DEFAULT
    CountRefs( ThrmlZn:VentSysRef )
  SIZING
    UNDEFINED
  ANNUAL
    CountRefs( ThrmlZn:VentSysRef )
ENDRULE
RULE ZnSys:SysRefStatus
  DESCRIPTION
    "Enumeration that indicates status of the ZnSys with respect its 
     assignment to the ThrmlZn:PriAirCondgSysRef properties."
  HELP
    "Used to control list of available ZnSys that can be assigned to the 
     ThrmlZn:PriAirCondgSysRef[X] property."
  INPUTCLASS
    NotInput
  OPTION
    HVAC
    Invalid
  DEFAULT
    if( NumAirCondgSysRefs >= 1 )
    then "Invalid"
    else "HVAC"
    endif
ENDRULE

// Status of ThermalZone assignments to AirSystems
RULE NEW AirSys:NumAirCondgSysRefs
  DATATYPE
    Integer
  LONGFORM
    NumberAirConditioningSystemReferences
  DESCRIPTION
    "The number of references to the AirSys via ThrmlZn:PriAirCondgSysRef property."  
  INPUTCLASS
    NotInput 
  DEFAULT
    CountRefs( ThrmlZn:PriAirCondgSysRef )
  SIZING
    UNDEFINED
  ANNUAL
    CountRefs( ThrmlZn:PriAirCondgSysRef )
ENDRULE
RULE NEW AirSys:NumVentSysRefs
  DATATYPE
    Integer
  LONGFORM
    NumberVentilationSystemReferences
  DESCRIPTION
    "The number of references to the AirSys via ThrmlZn:VentSysRef property."  
  INPUTCLASS
    NotInput 
  DEFAULT
    CountRefs( ThrmlZn:VentSysRef )
  SIZING
    UNDEFINED
  ANNUAL
    CountRefs( ThrmlZn:VentSysRef )
ENDRULE
RULE AirSys:SysRefStatus
  DESCRIPTION
    "Enumeration that indicates status of the AirSys with respect its 
     assignment to the ThrmlZn:PriAirCondgSysRef and VentSystRef properties."
  HELP
    "Used to control list of available AirSys that can be assigned to the 
     ThrmlZn:PriAirCondgSysRef and VentSysRef properties."
  INPUTCLASS
    NotInput
  OPTION
    HVAC
    Invalid
  DEFAULT
    "HVAC"     
  SIZING
    UNDEFINED
  ANNUAL
    z:SysRefStatus
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW ThrmlZn:ClgUMLHLimitSpcMax
  RULESETS
    T24N
  DATATYPE
    Integer
  LONGFORM
    CoolingUnmetLoadHourLimitSpaceMaximum
  DESCRIPTION
    "The maximum UMLH limit value from the spaces assigned to the zone."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    MaxRevRef( Spc:ThrmlZnRef, Spc:ClgUMLHLimit )
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW ThrmlZn:HtgUMLHLimitSpcMax
  RULESETS
    T24N
  DATATYPE
    Integer
  LONGFORM
    HeatingUnmetLoadHourLimitSpaceMaximum
  DESCRIPTION
    "The maximum UMLH limit value from the spaces assigned to the zone."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    MaxRevRef( Spc:ThrmlZnRef, Spc:HtgUMLHLimit )
ENDRULE


// -----------------------------------------------------------------------------
RULE ThrmlZn:ClgUMLHLimit
  RULESETS
    T24N
  DESCRIPTION
    "The limit on cooling unmet load hours (UMLH) for the zone, 
     based on the minimum value from the spaces in the zone."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    MinRevRef( Spc:ThrmlZnRef, Spc:ClgUMLHLimit )
  CHECKCODE
    if( ClgUMLHLimit != ClgUMLHLimitSpcMax ) then
      PostWarning("The spaces assigned to thermal zone '%s' have cooling unmet load
                   hour limits ranging from %g to %g. All spaces in the 
                   zone will be held to the minimum UMLH limit.", Name, 
                   ClgUMLHLimit, ClgUMLHLimitSpcMax)
    else UNCHANGED
    endif
  ANNUAL
    ClgUMLHLimit
ENDRULE



// -----------------------------------------------------------------------------
RULE ThrmlZn:HtgUMLHLimit
  RULESETS
    T24N
  DESCRIPTION
    "The limit on heating unmet load hours (UMLH) for the zone, 
     based on the minimum value from the spaces in the zone."
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT
    MinRevRef( Spc:ThrmlZnRef, Spc:HtgUMLHLimit )
  CHECKCODE
    if( HtgUMLHLimit != HtgUMLHLimitSpcMax ) then
      PostWarning("The spaces assigned to thermal zone '%s' have heating unmet load
                   hour limits ranging from %g to %g. All spaces in the 
                   zone will be held to the minimum UMLH limit.", Name, 
                   HtgUMLHLimit, HtgUMLHLimitSpcMax)
    else UNCHANGED
    endif
  ANNUAL
    HtgUMLHLimit
ENDRULE

// -------- Calculating Operable Window Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtOperableWinArea
  DATATYPE
    Float
  LONGFORM
    ExteriorOperableWindowArea
  DESCRIPTION
    "Area of all operable windows on the exterior wall in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtOperableWinArea )
ENDRULE


// -------- Calculating New Operable Window Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtNewOperableWinArea
  DATATYPE
    Float
  LONGFORM
    ExteriorNewOperableWindowArea
  DESCRIPTION
    "Area of all new operable windows on the exterior wall in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtNewOperableWinArea )
ENDRULE

// -------- Calculating Operable Skylight Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtOperableSkyltArea
  DATATYPE
    Float
  LONGFORM
    ExteriorOperableSkylightArea
  DESCRIPTION
    "Area of all operable skylight on the exterior wall in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtOperableSkyltArea )
ENDRULE


// -------- Calculating New Operable Skylight Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtNewOperableSkyltArea
  DATATYPE
    Float
  LONGFORM
    ExteriorNewOperableSkylightArea
  DESCRIPTION
    "Area of all new operable sklights on the roof in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtNewOperableSkyltArea )
ENDRULE



// -------- Calculating Door Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtDrArea
  DATATYPE
    Float
  LONGFORM
    ExteriorDoorArea
  DESCRIPTION
    "Area of all doors on the exterior wall in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtDrArea )
ENDRULE


// -------- Calculating New Door Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:ExtNewDrArea
  DATATYPE
    Float
  LONGFORM
    ExteriorNewDoorArea
  DESCRIPTION
    "Area of all new doors on the exterior wall in a conditioned 
     thermal zone."  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    SumRevRef( Spc:ThrmlZnRef, Spc:ExtNewDrArea )
ENDRULE



// -------- Heating Cooling Interlock - Used for screen control ------------------
RULE NEW ThrmlZn:OperableWinInterlockReq
  RULESETS
    T24N
  LONGFORM
    OperableWindowInterlockRequired
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT : T24N_2016
    if( IsAltOnly )
    then 0
    else if( IsAddOrAlt .AND. IsAlt )
    then
      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
          ( IfValidAnd(ExtNewOperableWinArea > 0) .OR. IfValidAnd(ExtNewDrArea > 0) .OR. IfValidAnd(ExtNewOperableSkyltArea >0) ) 
          .AND. ThrmlZn:IsNewHVAC = 1 )
      then 1
      else 0     
      endif
    else if( IsNewEnv .OR. IsAddEnv )
    then
      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
          ( IfValidAnd(ExtNewOperableWinArea > 0) .OR. IfValidAnd(ExtNewDrArea > 0) .OR. IfValidAnd(ExtNewOperableSkyltArea >0) ) )
      then 1
      else 0     
      endif
    else if( IsNewMech .OR. IsAddMech )
    then
      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
          ( IfValidAnd(ExtOperableWinArea > 0) .OR. IfValidAnd(ExtDrArea > 0) .OR. IfValidAnd(ExtOperableSkyltArea >0) ) )
      then 1
      else 0     
      endif
    else 0
    endif endif endif endif
  DEFAULT : T24N
    if( IfValidAnd( IsHlthCare = 1 ) .OR. IfValidAnd( IsHighRiseRes = 1 ) )
    then 0
    else  
	    if( IsAltOnly )
	    then 0
	    else if( IsAddOrAlt .AND. IsAlt )
	    then
	      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
	          ( IfValidAnd(ExtNewOperableWinArea > 0) .OR. IfValidAnd(ExtNewDrArea > 0) .OR. IfValidAnd(ExtNewOperableSkyltArea >0) ) 
	          .AND. ThrmlZn:IsNewHVAC = 1 )
	      then 1
	      else 0     
	      endif
	    else if( IsNewEnv .OR. IsAddEnv )
	    then
	      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
	          ( IfValidAnd(ExtNewOperableWinArea > 0) .OR. IfValidAnd(ExtNewDrArea > 0) .OR. IfValidAnd(ExtNewOperableSkyltArea >0) ) )
	      then 1
	      else 0     
	      endif
	    else if( IsNewMech .OR. IsAddMech )
	    then
	      if( IfValidAnd(ThrmlZn:Type = "Conditioned") .AND.
	          ( IfValidAnd(ExtOperableWinArea > 0) .OR. IfValidAnd(ExtDrArea > 0) .OR. IfValidAnd(ExtOperableSkyltArea >0) ) )
	      then 1
	      else 0     
	      endif
	    else 0
	    endif endif endif endif
    endif
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED
    u:OperableWinInterlockReq    
ENDRULE


// -------------- Heating Cooling Interlock Checkbox----------------------------
RULE ThrmlZn:OperableWinHaveInterlock
  DESCRIPTION
    "A flag to indicate that all operable windows interlock with heating and 
     cooling systems."
  HELP
    ""
  INPUTCLASS
    CondRequired
  DEFAULT : T24N_2016
    0  
  DEFAULT : T24N
    if( IfValidAnd( IsHlthCare = 1 ) .OR. IfValidAnd( IsHighRiseRes = 1 ) )
    then 1
    else 0
    endif
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED
    u:OperableWinHaveInterlock
ENDRULE


// -------------- Heating Cooling Interlock --------------
RULE ThrmlZn:OperableWinInterlock
  DESCRIPTION
    "A flag to indicate that not all new operable windows interlock with 
     heating and cooling systems and the penalty of additional OA flow will 
     be applied."
  HELP
    ""
  INPUTCLASS
    CondRequired
  DEFAULT
    if( IfValidAnd(OperableWinInterlockReq = 0 ) )
    then UNDEFINED
    else if( OperableWinHaveInterlock = 1 )
    then 
      if( IfValidAnd(HasUnknownHVAC = 1) )
      then 3
      else 0     // no penalty
      endif
    else if( IfValidAnd(HasUnknownHVAC = 0) )
    then 1      // apply the penalty
    else 2      // apply the warning
    endif endif endif
  CHECKSIM
    if( IfValidAnd(OperableWinInterlock = 2) )
    then    
      PostWarning("Thermal zone '%s' has operable windows which are required 
                   to be interlocked with the HVAC system. Such interlocks are
                   not shown to be included in the design.",Name)
    else UNCHANGED
    endif
  SIZING
    UNDEFINED
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED
    u:OperableWinInterlock
ENDRULE

// -------- Flag for triggering warning for Thermal Zone where interlock is 
//          required but not provided for non-mechanical compliance types  --------------
RULE NEW ThrmlZn:OperableWinInterlockWarn1
  DATATYPE
    Integer
  LONGFORM
    OperableWinInterlockWarning1
  DESCRIPTION
    ""  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    if( IfValidAnd(OperableWinInterlock = 2 ) )
    then 1
    else 0
    endif
ENDRULE

// -------- Flag for triggering warning for Thermal Zone where interlock is 
//          required and provided for non-mechanical compliance types  --------------
RULE NEW ThrmlZn:OperableWinInterlockWarn2
  DATATYPE
    Integer
  LONGFORM
    OperableWinInterlockWarning2
  DESCRIPTION
    ""  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  DEFAULT   
    if( IfValidAnd(OperableWinInterlock = 3 ) )
    then 1
    else 0
    endif
ENDRULE

// --------------Outdoor Air Flow Rate for Zone Ventilation Object for Interlock  --------------
RULE ThrmlZn:OperableWinOAFlow
  DESCRIPTION
    ""
  HELP
    ""
  INPUTCLASS
    NotInput
  DEFAULT : T24N_2016
    if( LocalStatus( OperableWinInterlock ) < 1 )
    then UNDEFINED
    else if( OperableWinInterlock = 1 .AND. IsRes = 1 )
    then
      ValidOr(ThrmlZn:DirectCondArea,0) * 0.02   // penalty of 0.02 cfm/ft2 as per CEC
    else if( OperableWinInterlock = 1 .AND. IsRes = 0 )
    then
      ValidOr(ThrmlZn:DirectCondArea,0) * 0.15   // penalty of 0.15 cfm/ft2 as per CASE report
    else
      UNDEFINED
    endif endif endif
  DEFAULT : T24N
    if( LocalStatus( OperableWinInterlock ) < 1 )
    then UNDEFINED
     else if( OperableWinInterlock = 1 .AND. ( IsHighRiseRes = 0 .OR. IsHlthCare = 0) )
    then
      ValidOr(ThrmlZn:DirectCondArea,0) * 0.15   // penalty of 0.15 cfm/ft2 as per CASE report
    else
      UNDEFINED
    endif endif
  SIZING
    UNDEFINED
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED : T24N_2016
    u:OperableWinOAFlow
  ANNUAL_PROPOSED : T24N
    u:OperableWinOAFlow
ENDRULE


// -------------- Operable Window Penalty for no Interlock  - High OA Limit ----
RULE ThrmlZn:OperableWinOALimHi
  DESCRIPTION
    "The high temperature limit, below which the outdoor air penalty will 
     operate in zones which have operable windows without interlocks."
  INPUTCLASS
    Prescribed
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED : T24N
    if( IfValidAnd(OperableWinInterlockReq = 0 ) )
    then UNDEFINED
    else if( OperableWinHaveInterlock = 1 )
    then UNDEFINED
    else if( OperableWinInterlock = 2 )
    then UNDEFINED
    else 90
    endif endif endif
ENDRULE


// -------------- Operable Window Penalty for no Interlock  - Low OA Limit ----
RULE ThrmlZn:OperableWinOALimLo
  DESCRIPTION
    "The low temperature limit, above which the outdoor air penalty will 
     operate in zones which have operable windows without interlocks."
  INPUTCLASS
    Prescribed
  ANNUAL_BASELINE
    UNDEFINED
  ANNUAL_PROPOSED : T24N
    if( IfValidAnd(OperableWinInterlockReq = 0 ) )
    then UNDEFINED
    else if( OperableWinHaveInterlock = 1 )
    then UNDEFINED
    else if( OperableWinInterlock = 2 )
    then UNDEFINED    
    else 50   // lower OA limit based on the CASE report
    endif endif endif
ENDRULE

// ----------------- Window Interlock Report -------------------------------------
RULE ThrmlZn:WinInterlockRpt
  RULESETS
    T24N
  INPUTCLASS
    NotInput
  ANNUAL_PROPOSED
     if ( IfValidAnd(OperableWinInterlockReq = 0)  )
     then "NA"
     else if ( IfValidAnd(OperableWinHaveInterlock = 1) )
      then "Y"
     else "N"
     endif endif
ENDRULE

// -------- Calculating New Door Area for Interlocks at Zone  --------------
RULE NEW ThrmlZn:WinInterlockRptFlag
  DATATYPE
    Integer
  LONGFORM
    WindowInterlockReportFlag
  DESCRIPTION
    ""  
  HELP
    ""  
  INPUTCLASS
    NotInput   
  ANNUAL_PROPOSED   
    if ( WinInterlockRpt = "NA"  )
    then 0
    else if ( WinInterlockRpt = "Y"  )
    then 1
    else -1
    endif endif
ENDRULE



// List of ventilation function(s) included in Thermal Zone
RULE ThrmlZn:VentFuncList
  DESCRIPTION
    ""
  INPUTCLASS 
    NotInput
  ANNUAL : T24N_2016
    UNDEFINED
  ANNUAL
    UniqueListRevRef( Spc:ThrmlZnRef, "%s", "\n%s", "\n%s", Spc:VentSpcFunc )
ENDRULE

RULE ThrmlZn:HotelMotelGuestRmCnt
  DESCRIPTION
    "The number of Hotel/Motel Guest Rooms in the ThermalZone"
  INPUTCLASS 
    NotInput
  DEFAULT
    SumRevRef( Spc:ThrmlZnRef, Spc:HotelMotelGuestRmCnt )
  ANNUAL
    SumRevRef( Spc:ThrmlZnRef, Spc:HotelMotelGuestRmCnt )
ENDRULE

RULE ThrmlZn:ResLivingBdrmCnt
  DESCRIPTION
    "The number of bedrooms in Thermal Zone"
  INPUTCLASS 
    NotInput
  DEFAULT
    SumRevRef( Spc:ThrmlZnRef, Spc:ResLivingBdrmCnt )
  ANNUAL
    SumRevRef( Spc:ThrmlZnRef, Spc:ResLivingBdrmCnt )
ENDRULE

RULE NEW ThrmlZn:ResLivingUnitCnt
  DATATYPE
    Integer
  DESCRIPTION
    "The number of Residential Living Units in the ThermalZone"
  INPUTCLASS 
    NotInput
  DEFAULT
    SumRevRef( Spc:ThrmlZnRef, Spc:ResLivingUnitCnt )
ENDRULE

RULE NEW ThrmlZn:ResLivingUnitCntWithMult
  DATATYPE
    Integer
  DESCRIPTION
    "The number of Residential Living Units in the ThermalZone, with multiplier"
  INPUTCLASS 
    NotInput
  DEFAULT
    ResLivingUnitCnt * Mult
ENDRULE

// -------- HRR Check for accessible ventilation equipment  --------------
RULE NEW ThrmlZn:HRRVentSysRulesApply
  DATATYPE
    Integer
  DESCRIPTION
    "A flag that indicates that HRR ventilation system rules apply to the zone."
  HELP
    "This flag is 1 if this is a HRR zone with a new ventilation system 
     and any of the following apply:
     a) there is a 1:1 relationship between the # of systems and dwelling units
     b) the ventilations system is Type = 'VentilationOnly', or
     c) the system has heat recovery and it is certification is 'HVI'."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( VentSysRef:IsNew > 0 ) .AND.
        IfValidAnd( UniqueVentSysAssigned > 0 ) .AND.
        IfValidAnd( VentSysRef:IsHRRVentSys > 0 ) .AND.
        ( IfValidAnd( VentSysRef:IsUnitaryHRRVentSys > 0 ) .OR.
          IfValidAnd( VentSysRef:Type = "VentilationOnly" ) .OR.
          IfValidAnd( VentSysRef:HtRcvryRef:HtRcvryCertification = "HVI" ) ) )
    then 1
    else 0
    endif
ENDRULE

RULE NEW ThrmlZn:ResVentEquipChkReq
  DATATYPE
    Integer
  LONGFORM
    ResidentialVentilationEquipmentCheckRequired
  DESCRIPTION
    "A flag that indicates that ResVentEquipIsAccessible check is required."
  HELP
    "This check is applicable if HRRVentSysRulesApply = 1 and the ventilation
     system serving it is supply or balanced."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( HRRVentSysRulesApply > 0 ) .AND.
        ( PropVentSysIsSup > 0 .OR. PropVentSysIsBalanced > 0 ) )
    then 1
    else 0
    endif
ENDRULE

RULE ThrmlZn:ResVentEquipIsAccessible
  DESCRIPTION
    "A flag that indicates whether single-zone IAQ system supply air filters, 
     outside air inlets, and H/ERV recovery cores (if applicable) are accessible
     according to the criteria set in the RACM.
     If not checked, the Standard Design ventilation system is modeled the 
     same as the Proposed with limitations on system fan power."
  INPUTCLASS 
    Default
  DEFAULT
    0
ENDRULE

RULE NEW ThrmlZn:ResVentFIDChkReq
  DATATYPE
    Integer
  LONGFORM
    ResidentialVentilationFaultIndicationDisplayCheckRequired
  DESCRIPTION
    "A flag that indicates that ResVentEquipHasFID check is required."
  HELP
    "This check is applicable if HRRVentSysRulesApply = 1 and the ventilation
     system serving it is supply or balanced."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( IfValidAnd( HRRVentSysRulesApply > 0 ) .AND.
        ( PropVentSysIsSup > 0 .OR. PropVentSysIsBalanced > 0 ) )
    then 1
    else 0
    endif
ENDRULE

RULE ThrmlZn:ResVentEquipHasFID
  DESCRIPTION
    "A flag that indicates whether single-zone IAQ system have fault indication
     display according to the criteria set in the RACM.
     If not checked, penalty adjustments to fan power and heat recovery
     effectiveness will be applied to the Proposed system."
  INPUTCLASS 
    Default
  DEFAULT
    0
ENDRULE