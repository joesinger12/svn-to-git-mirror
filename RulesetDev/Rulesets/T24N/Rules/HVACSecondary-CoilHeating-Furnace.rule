// HVAC Secondary Systems - Heating Coils - Furnaces
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014-2017, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:



// ---------- Section 5.7.6.4 - Furnaces ---------------------------------------
// ********** Furnace Fuel Heating Efficiency (AFUE) ***************************
RULE NEW CoilHtg:CapTotInpRtd
  DATATYPE
    Float
  LONGFORM
    CapacityTotalInputRated
  DESCRIPTION
    "The rated input for the furnace. Used in determining efficinecy requirements."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( Type = "Furnace" .AND. IfValidAnd( CapTotGrossRtd > 0 ) )
    then 
      if( IfValidAnd( FurnThrmlEff > 0 ) )
      then // Calculate input ( efficiency from ThrmlEff )
        CapTotGrossRtd / FurnThrmlEff
      else if( IfValidAnd( FurnAFUE > 0 ) )
      then // Calculate input ( efficiency from AFUE )
        CapTotGrossRtd / ( ( 0.0051427 * ( FurnAFUE * 100 ) ) + 0.3989 )
      else CapTotGrossRtd / 0.80 // This is an intermediate default
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( Type = "Furnace" .AND. IfValidAnd( CapTotGrossRtd > 0 ) )
    then 
      if( IfValidAnd( FurnThrmlEff > 0 ) )
      then // Calculate input ( efficiency from ThrmlEff )
        CapTotGrossRtd / FurnThrmlEff
      else if( IfValidAnd( FurnAFUE > 0 ) )
      then // Calculate input ( efficiency from AFUE )
        CapTotGrossRtd / ( ( 0.0051427 * ( FurnAFUE * 100 ) ) + 0.3989 )
      else CapTotGrossRtd / 0.80 // This is the default for baseline
      endif endif
    else UNDEFINED
    endif
ENDRULE



// ********** Furnace Fuel Heating Efficiency (AFUE) ***************************
RULE NEW CoilHtg:CodeMinAFUE
  DATATYPE
    Float
  LONGFORM
    CodeMinimumAFUE
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "Furnace" .AND. IfValidAnd( CapTotInpRtd > 0 ) )
    then // ------------------------------------------------------------------
      if( ParentComp = "AirSys" )
      then // AirSys
        if( CapTotInpRtd < 225000 )
        then // AFUE rating threshold
          if( IfValidAnd( AirSys:SubType = "Packaged1Phase" ) .OR. IfValidAnd( AirSys:SubType = "Packaged3Phase" ) )
          then 0.81 // Table E-6, gas, weatherized
          else
          if( IfValidAnd( AirSys:SubType = "Split1Phase" ) .OR. IfValidAnd( AirSys:SubType = "Split3Phase" ) )
          then 0.80 // Table E-6, non-weatherized
          else 0.81 // All other, assume weatherized. Was 0.78, which was incorrect. See Ticket 3445
          endif endif
        else UNDEFINED // >=225MBH uses ThrmlEff
        endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // ZnSys
        // First, types covered by 2016 Appliance Std, Table E-2
        if( IfValidAnd( ZnSys:SubType = "Wall" ) )
        then 
          if( IfValidAnd( ZnSys:FurnaceType = "Gravity" ) )
          then // Wall, Gravity. See issue 2173 
            if( CapTotInpRtd <= 27000 )
            then 0.65
            else
            if( CapTotInpRtd <= 46000 )
            then 0.66
            else 0.67
            endif endif
          else // Wall, Fan
          if( CapTotInpRtd <= 42000 )
          then 0.75
          else 0.76
          endif endif
        else
        if( IfValidAnd( ZnSys:SubType = "Floor" ) )
        then // Floor, All
          if( CapTotInpRtd <= 37000 )
          then 0.57
          else 0.58
          endif 
        else
        if( IfValidAnd( ZnSys:SubType = "Room" ) )
        then // Room, All
          if( CapTotInpRtd <= 20000 )
          then 0.61
          else
          if( CapTotInpRtd <= 27000 )
          then 0.66
          else
          if( CapTotInpRtd <= 46000 )
          then 0.67                 
          else 0.68
          endif endif endif               
        else
        if( IfValidAnd( ZnSys:SubType = "Other" ) )
        then // See Issue 2561, which supercedes 2173
          if( Proj:StdsVersionYr >= 2016 )
          then 0.80 // Compliance2016 and beyond. Assume 1-phase non-weatherized for this case
          else 0.78 // Before Compliance2016
          endif
        else
        // All other furnaces
        if( CapTotInpRtd < 225000 )
        then
          if( IfValidAnd( ZnSys:SubType = "Packaged1Phase" ) .OR. IfValidAnd( ZnSys:SubType = "Packaged3Phase" ) )
          then 0.81 // Table E-6, gas, weatherized
          else
          if( IfValidAnd( ZnSys:SubType = "Split1Phase" ) .OR. IfValidAnd( ZnSys:SubType = "Split3Phase" ) )
          then 0.80 // Table E-6, non-weatherized
          else 0.81 // All other, assume weatherized. Was 0.78, which was incorrect. See Ticket 3445
          endif endif
        else UNDEFINED // >=225MBH uses ThrmlEff
        endif endif endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N_2019
    if( BaseSysNum > 0 .AND. Type = "Furnace" )
    then
      if( BaseSysNum = 1 )
      then 0.80 // H/M Guestrooms and HRR is Split1Phase (non-weatherized).
      else if( IfValidAnd( CapTotInpRtd < 225000 ) )
      then 0.81 // Assume baseline is Packaged3Phase (weatherized).
                // See tic 2561, which supercedes 2173, and then finally tic 3445
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "Furnace" )
    then
// See Appl Stds Table E-6
      if( BaseSysNum = 1 .OR. BaseSysNum > 200 )
      then 0.80 // H/M Guestrooms and MFam Common is Split1Phase (non-weatherized). See Issue 3432/3431, and finally 3450
      else if( IfValidAnd( CapTotInpRtd < 225000 ) )
      then 0.81 // Assume baseline is Packaged3Phase (weatherized).
                // See tic 2561, which supercedes 2173, and then finally tic 3445
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:FurnAFUE
  DESCRIPTION
    "The Annual Fuel Utilization Efficiency (AFUE) of a furnace at AHRI rated conditions."
  HELP
    "This is a required input if the gross input capacity is < 225 MBH."
  REFERENCE 
    NACM Section 5.7.6.4
    T24 Std Table 110.2-J
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    0.57
  COMMONMAXIMUM
    0.98
  MAXIMUM
    1.0
  DEFAULT
    if( Proj:AutoEffInput = 1 .AND.
        IfValidAnd( CodeMinAFUE > 0 ) )
    then // For PROPOSED AutoEffInput only 
      CodeMinAFUE
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinAFUE > 0 ) .AND. 
        IfValidAnd( FurnAFUE > 0 ) = 0 )
    then 
      PostError("Furnace coil '%s' must be specified with AHRI rated AFUE.", Name )
    else
    if( IfValidAnd( FurnAFUE < CodeMinAFUE ) )
    // FurnAFUE is defined and FurnThrmlEff is not user-defined. If FurnThrmlEff is defined, see CHECK for that rule
    then 
      PostError("The AFUE of Furnace coil '%s' is less than the code minimum required efficiency
                 of AFUE %.2f. Select the bypass of efficiency check at the coil
                 if this is not applicable.", Name, CodeMinAFUE )
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    if( IfValidAnd( CodeMinAFUE > 0 ) .AND. 
        IfValidAnd( FurnAFUE > 0 ) = 0 )
    then 
      PostError("Furnace coil '%s' must be specified with AHRI rated AFUE.", Name )
    else UNCHANGED 
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "Furnace")
    then UNDEFINED
    else FurnAFUE
    endif
  ANNUAL : T24N_2016
    if( BaseSysNum > 0 .AND. Type = "Furnace" )
    then
      if( IfValidAnd( CapTotInpRtd < 225000 ) )
      then 0.78 // Assume baseline is 3 phase. See Issue 2561, which supercedes 2173
      else UNDEFINED
      endif
    else z:FurnAFUE
    endif
  ANNUAL
    if( ( BaseSysNum > 0 .OR. IsBaseHlthCare > 0 ) .AND.
        Type = "Furnace" )
    then // Is baseline system
      if( IfValidAnd( CodeMinAFUE > 0 ) )
      then CodeMinAFUE
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE


// ********** Furnace Fuel Heating Efficiency (Thrml Eff) **********************
RULE NEW CoilHtg:CodeMinThrmlEff
  DATATYPE
    Float
  LONGFORM
    CodeMinimumThrmlEff
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "Furnace" .AND. IfValidAnd( CapTotInpRtd > 0 ) )
    then
      if( CapTotInpRtd < 225000 )
      then UNDEFINED // AFUE is only efficiency metric for <225 kBtu/hr, per email from LF on 9/11/17
      else
      if( Proj:StdsVersionYr < 2022 )
      then // Prior to 1/1/2023
        if( FuelSrc = "Oil" ) 
        then 0.81
        else 0.80
        endif
      else
      if( FuelSrc = "Oil" ) 
      then 0.82
      else 0.81
      endif endif endif
    else UNDEFINED
    endif
  ANNUAL : T24N_2019
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then 
      if( CapTotInpRtd >= 225000 )
      then 0.80 // Gas furnace Appl Stds Table E-5, prior to 1/1/23
      else UNDEFINED
      endif
    else UNCHANGED
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then 
      if( CapTotInpRtd >= 225000 )
      then 0.81 // Gas furnace Appl Stds Table E-5, eff 1/1/23
      else UNDEFINED
      endif 
    else UNCHANGED
    endif
ENDRULE

/
// -----------------------------------------------------------------------------
RULE CoilHtg:FurnThrmlEff
  DESCRIPTION
    "The thermal efficiency of a furnace at AHRI rated conditions."
  HELP
    "This is a required input if the gross input capacity is >=225 MBH."
  REFERENCE 
    NACM Section 5.7.6.5
    T24 Std Table 110.2-J
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    0.6
  COMMONMAXIMUM
    0.98
  MAXIMUM
    1.0
  DEFAULT
    if( Type = "Furnace" )
    then 
      if( Proj:AutoEffInput = 1 .AND. 
          IfValidAnd( CodeMinThrmlEff > 0 ) )
      then // For PROPOSED AutoEffInput only 
        CodeMinThrmlEff
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( ( BypassMinEffCheck > 0 .OR. BypassCheckCode = 1 )  
        .OR. 
        ( BypassCheckCode = 2 .AND. BypassMinEffCheck > 0 ) )
    then // Is a non-healthcare system w/ BypassMinEff or CheckCode applicable
         // Or a new healthcare system w/ BypassMinEff checked
      UNCHANGED
    else
    if( IfValidAnd( CodeMinThrmlEff > 0 ) .AND.
        IfValidAnd( FurnThrmlEff > 0 ) = 0 )
    then
      PostError("Furnace coil '%s' must be specified with AHRI rated thermal efficiency.", Name )
    else 
    if( IfValidAnd( FurnThrmlEff < CodeMinThrmlEff ) )
    then
      PostError("Thermal efficiency of coil '%s' is less than the code minimum
                 required efficiency of %.2f. Select the bypass of
                 efficiency check at the coil if this is not applicable.",
                 Name, CodeMinThrmlEff)
    else UNCHANGED 
    endif endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    if( IfValidAnd( CodeMinThrmlEff > 0 ) .AND.
        IfValidAnd( FurnThrmlEff > 0 ) = 0 )
    then 
      PostError("Furnace coil '%s' must be specified with AHRI rated AFUE or 
                thermal efficiency.", Name )
    else UNCHANGED 
    endif
  SIZING
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then 0.8 // Default assumption for sizing
    else if( Type = "Furnace" )
    then
      if( IfValidAnd( CodeMinAFUE > 0 ) .AND. IfValidAnd( FurnAFUE > 0 ) )
      then // Is AFUE rated, use equation to calculate ThrmlEff from AFUE
        ( 0.0051427 * ( FurnAFUE * 100 ) ) + 0.3989
      else FurnThrmlEff
      endif
    else UNDEFINED
    endif endif
  ANNUAL
    if( ( BaseSysNum > 0 .OR. IsBaseHlthCare > 0 ) .AND.
        Type = "Furnace" )
    then 
      if( IfValidAnd( CodeMinAFUE > 0 ) )
      then // Is AFUE rated, use equation to calculate ThrmlEff from AFUE
        ( 0.0051427 * ( FurnAFUE * 100 ) ) + 0.3989
      else CodeMinThrmlEff
      endif
    else UNCHANGED
    endif
ENDRULE


// ********** Furnace Fuel Heating Part Load Efficiency Curve ******************
RULE CoilHtg:FurnHIR_fPLRCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency as a function of 
     part-load ratio."
  REFERENCE 
    NACM Section 5.7.6.4
  INPUTCLASS
    Prescribed  
  SIZING
    if( Type = "Furnace" )
    then RuleLibrary(CrvQuad, "CoilHtgFurnFIRRatio_fQRatio") 
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then RuleLibrary(CrvQuad, "CoilHtgFurnFIRRatio_fQRatio") 
    else UNCHANGED
    endif
ENDRULE


// ********** Furnace Fuel Heating Pilot ***************************************
RULE CoilHtg:FurnIgnType
  DESCRIPTION
    "The method used to start combustion in fuel-fired furnaces."
  REFERENCE 
    NACM Section 5.7.6.4
  INPUTCLASS 
    Default
  OPTION
    IntermittentIgnitionDevice
    PilotLight
  DEFAULT
    if( Type = "Furnace" )
    then "IntermittentIgnitionDevice"
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then "IntermittentIgnitionDevice"
    else if( Type = "Furnace" )
    then FurnIgnType
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then "IntermittentIgnitionDevice"
    else if( Type = "Furnace" )
    then FurnIgnType
    else UNDEFINED
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:FurnPilotFuelInp
  DESCRIPTION
    "The fuel input for a pilot light on a furnace."
  REFERENCE 
    NACM Section 5.7.6.4
  INPUTCLASS 
    Default
  DEFAULT
    if( Type = "Furnace" .AND. 
        IfValidAnd( FurnIgnType = "PilotLight" ) .AND.
        LocalStatus ( CapTotGrossRtd ) > 0 )
    then CapTotGrossRtd * 0.01 // Pilot is ~1% of gross output capacity.
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then UNDEFINED
    else if( Type = "Furnace" )
    then FurnPilotFuelInp
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "Furnace")
    then UNDEFINED
    else if( Type = "Furnace" )
    then FurnPilotFuelInp
    else UNDEFINED
    endif endif
ENDRULE


// ********** Furnace Fuel Heating Fan/Auxiliary *******************************
RULE CoilHtg:AuxPwr
  DESCRIPTION
    "The fan energy in forced draft furnaces and the auxiliary (pumps and outdoor
     fan) energy in fuel-fired heat pumps."
  REFERENCE 
    NACM Section 5.7.6.4
  PREVIOUSNAMES
    AuxilliaryPower
  INPUTCLASS 
    Optional 
  UNITS
    W
  DEFAULT
    0
  SIZING
    if( BaseSysNum > 0 )
    then 0
    else AuxPwr
    endif
  ANNUAL
    if( BaseSysNum > 0 )
    then 0
    else AuxPwr
    endif
ENDRULE

